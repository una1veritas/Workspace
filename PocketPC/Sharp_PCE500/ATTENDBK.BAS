100 REM ATTENDBK
110 DIM SID(100),NAM$(100)*12,PRS$(100)*18,DT(20)
120 NUM=0: DTE=0
130 IF NUM=0 GOSUB 740:GOSUB 1110
140 IF DTE=0 GOSUB 1240
150 CLS
160 LOCATE 0,3:PRINT "BROWSE   SAVE    DATE             QUIT"
170 LOCATE 32,2:S$=STR$ (DT(DTE)):PRINT MID$ (S$,6,2);"/";MID$ (S$,8,2)
180 K$=INKEY$
190 IF K$="B" OR ASC (K$)=241 GOSUB 250:GOTO 150
200 IF K$="S" OR ASC (K$)=242 GOSUB 890:GOTO 150
210 IF K$="D" OR ASC (K$)=243 GOSUB 1240:GOTO 150
220 IF ASC (K$)=245 THEN INPUT "QUIT?";Q$:IF Q$="Y" THEN END
230 GOTO 180
240 END
250 REM SUB. SHOW STUDENT LIST
260 CLS
270 I=1
280 D=DTE
290 LOCATE 0,0
300 PRINT USING "### ######## &&&&&&&&&&&&&&&&&&&&&&";I;SID(I);NAM$(I);
310 LOCATE 1,1:PRINT PRS$(I)
320 LOCATE D-1,2:PRINT " ^";D;" "
330 LOCATE 32,2:S$=STR$ (DT(D)):PRINT MID$ (S$,6,2);"/";MID$ (S$,8,2)
340 K$=INKEY$ :IF K$<>"" THEN 340
350 K$=INKEY$ :IF K$="" THEN 350
360 IF (K$="N" OR ASC (K$)=5) AND I<NUM THEN I=I+1:GOTO 280
370 IF (K$="B" OR ASC (K$)=4) AND I>1 THEN I=I-1:GOTO 280
380 IF K$="R" THEN GOSUB 570:GOTO 280
390 IF K$="G" THEN GOSUB 510:GOTO 280
400 IF K$="P" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"P"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
410 IF K$="A" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"A"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
420 IF K$="L" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"L"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
430 IF K$="F" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"F"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
440 IF K$="Q" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"Q"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
450 IF K$="C" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"_"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
460 IF (ASC (K$)=15)AND D>1 THEN D=D-1:GOTO 290
470 IF (ASC (K$)=14)AND D<16 THEN D=D+1:GOTO 290
480 IF K$="X" THEN RETURN
490 GOTO 350
500 RETURN
510 REM SUB. GOTO ID
520 LOCATE 0,3: INPUT "ID=";ID
530 GOSUB 990
540 LOCATE 0,3: PRINT "            "
550 I=X
560 RETURN
570 REM SUB. RANDOM SKIP
580 LOCATE 0,3:PRINT "RANDOM..."
590 CNT=0:CNUM=NUM
600 FOR J=1TO NUM
610 IF MID$ (PRS$(J),DTE,1)<>"_" THEN CNUM=CNUM-1
620 NEXT J
630 IF CNUM=0 THEN GOTO 720
640 SKIP=RND (CNUM)
650 IX=1
660 J=I+IX
670 IF J>NUM THEN J=J-NUM:GOTO 670
680 IF MID$ (PRS$(J),DTE,1)="_"THEN CNT=CNT+1
690 IF CNT=SKIP THEN I=J: GOTO 720
700 IX=IX+1
710 GOTO 660
720 LOCATE 0,3:PRINT "         "
730 RETURN
740 REM SUB. READ MEIBO.DAT
750 CLS
760 PRINT "READING MEIBO.DAT"
770 NUM=0
780 OPEN "F:MEIBO.DAT" FOR INPUT AS #1
790 INPUT #1,ID,CS$,NA$,GD$
800 IF ID=0 THEN GOTO 870
810 NUM=NUM+1
820 SID(NUM)=ID
830 NAM$(NUM)=NA$+", "+GD$
840 LOCATE 0,1
850 PRINT USING "### ########";NUM;SID(NUM)
860 GOTO 790
870 CLOSE #1
880 RETURN
890 REM SUB. WRITE PRS$() TO ATTEND.DAT
900 CLS :PRINT "WRITIG ATTEND.DAT"
910 OPEN "F:ATTEND.DAT" FOR OUTPUT AS #2
920 FOR I=1 TO NUM
930 PRINT #2,SID(I);",";CHR$ (34);PRS$(I);CHR$ (34)
940 LOCATE 0,1:PRINT SID(I);" ";PRS$(I)
950 NEXT I
960 CLOSE #2
970 RETURN
980 REM 
990 REM SUB SERCH X BY SID(X)=ID
1000 L=0:R=NUM
1010 IF L=R THEN X=L:GOTO 1080
1020 X=INT ((L+R)/2)
1030 IF SID(X)=ID THEN GOTO 1080
1040 IF SID(X)<ID THEN L=X+1
1050 IF SID(X)>ID THEN R=X
1060 LOCATE 0,3:PRINT "[";L;",";R;"]"
1070 GOTO 1010
1080 LOCATE 0,3:PRINT SID(X)
1090 RETURN
1100 REM 
1110 REM READ ATTEND.DAT
1120 OPEN "F:ATTEND.DAT"FOR INPUT AS #2
1130 CLS
1140 FOR I=1 TO NUM
1150 LOCATE 0,0:PRINT I;"/";NUM
1160 INPUT #2,ID,P$
1170 LOCATE 0,1:PRINT ID;" ";P$
1180 IF SID(I)=ID THEN PRS$(I)=P$:GOTO 1200
1190 GOSUB 990:IF SID(X)=ID THEN PRS$(X)=P$
1200 NEXT I
1210 CLOSE #2
1220 RETURN
1230 REM 
1240 REM SUB SET DATE NUM TO DTE
1250 CLS
1260 RESTORE 1390
1270 FOR I=1 TO 16
1280 READ D,X
1290 IF D=0 THEN GOTO 1320
1300 DT(X)=D
1310 NEXT
1320 LOCATE 0,0:INPUT "DAY (YYYYMMDD)?",K$:MD=VAL (K$)
1330 RESTORE 1390
1340 FOR I=1 TO 16
1350 IF DT(I)=0 THEN GOTO 1380
1360 IF DT(I) >= MD THEN DTE=I:GOTO 1380
1370 NEXT I
1380 RETURN
1390 DATA 20170613,1,20170615,2
1400 DATA 20170620,3,20170622,4
1410 DATA 20170627,5,20170629,6
1420 DATA 20170704,7,20170706,8
1430 DATA 20170711,9,20170713,10
1440 DATA 20170718,11,20170720,12
1450 DATA 20170725,13,20170727,14
1460 DATA 20170801,15,20170803,16
1470 DATA 0,0
