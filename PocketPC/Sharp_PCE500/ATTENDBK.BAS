100 REM ATTENDBK
110 DIM SID(100),NAM$(100)*12,PRS$(100)*18,DT(20)
120 NUM=0: DTE=0
130 IF NUM=0 GOSUB 740:GOSUB 1120
140 IF DTE=0 GOSUB 1250
150 CLS
160 LOCATE 0,3:PRINT "R)EAD   B)ROWSE  S)AVE   D)ATE"
170 LOCATE 24,2:S$=STR$ (DT(DTE)):PRINT MID$ (S$,1,LEN (S$)-2);"/";MID$ (S$,LEN (S$)-1,2)
180 K$=INKEY$
190 IF K$="R" OR ASC (K$)=241 GOSUB 1120:GOTO 150
200 IF K$="B" OR ASC (K$)=242 GOSUB 250:GOTO 150
210 IF K$="S" OR ASC (K$)=243 GOSUB 890:GOTO 150
220 IF K$="D" OR ASC (K$)=244 GOSUB 1250:GOTO 150
230 GOTO 180
240 END
250 REM SUB. SHOW STUDENT LIST
260 CLS
270 I=1
280 D=DTE
290 LOCATE 0,0
300 PRINT USING "### ######## &&&&&&&&&&&&&&&&&&&&&&";I;SID(I);NAM$(I);
310 LOCATE 0,1:PRINT PRS$(I)
320 LOCATE 0,2:PRINT "123456789ABCDEFG :";D;
330 LOCATE 24,2:S$=STR$ (DT(D)):PRINT MID$ (S$,1,LEN (S$)-2);"/";MID$ (S$,LEN (S$)-2+1,2)
340 K$=INKEY$ :IF K$<>"" THEN 340
350 K$=INKEY$ :IF K$="" THEN 350
360 IF (K$="N" OR ASC (K$)=5) AND I<NUM THEN I=I+1:GOTO 280
370 IF (K$="B" OR ASC (K$)=4) AND I>1 THEN I=I-1:GOTO 280
380 IF K$="R" THEN GOSUB 570:GOTO 280
390 IF K$="G" THEN GOSUB 510:GOTO 280
400 IF K$="P" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"P"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
410 IF K$="A" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"A"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
420 IF K$="L" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"L"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
430 IF K$="F" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"F"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
440 IF K$="Q" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"Q"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
450 IF K$="C" THEN PRS$(I)=MID$ (PRS$(I),1,D-1)+"_"+MID$ (PRS$(I),D+1,LEN (PRS$(I))):GOTO 290
460 IF (ASC (K$)=15)AND D>1 THEN D=D-1:GOTO 290
470 IF (ASC (K$)=14)AND D<16 THEN D=D+1:GOTO 290
480 IF K$="X" THEN RETURN
490 GOTO 350
500 RETURN
510 REM SUB. INPUT ID
520 LOCATE 0,3: INPUT "No ";I
530 IF I < 1 THEN I = 1
540 IF I > NUM THEN I = NUM
550 LOCATE 0,3: PRINT "          "
560 RETURN
570 REM SUB. RANDOM SKIP
580 LOCATE 0,3:PRINT "RANDOM..."
590 CNT=0:CNUM=NUM
600 FOR J=1TO NUM
610 IF MID$ (PRS$(J),DTE,1)<>"_" THEN CNUM=CNUM-1
620 NEXT J
630 IF CNUM=0 THEN GOTO 720
640 SKIP=RND (CNUM)
650 IX=1
660 J=I+IX
670 IF J>NUM THEN J=J-NUM:GOTO 670
680 IF MID$ (PRS$(J),DTE,1)="_"THEN CNT=CNT+1
690 IF CNT=SKIP THEN I=J: GOTO 720
700 IX=IX+1
710 GOTO 660
720 LOCATE 0,3:PRINT "         "
730 RETURN
740 REM SUB. READ MEIBO.DAT
750 CLS
760 PRINT "READING MEIBO.DAT"
770 NUM=0
780 OPEN "F:MEIBO.DAT" FOR INPUT AS #1
790 INPUT #1,ID,CS$,NA$,GD$
800 IF ID=0 THEN GOTO 870
810 NUM=NUM+1
820 SID(NUM)=ID
830 NAM$(NUM)=NA$+", "+GD$
840 LOCATE 0,1
850 PRINT USING "### ########";NUM;SID(NUM)
860 GOTO 790
870 CLOSE #1
880 RETURN
890 REM SUB. WRITE PRS$() TO ATTEND.DAT
900 CLS :PRINT "WRITIG ATTEND.DAT"
910 OPEN "F:ATTEND.DAT" FOR OUTPUT AS #2
920 FOR I=1 TO NUM
930 PRINT #2,SID(I);",";CHR$ (34);PRS$(I);CHR$ (34)
940 LOCATE 0,1:PRINT SID(I);" ";PRS$(I)
950 NEXT I
960 CLOSE #2
970 RETURN
980 REM 
990 REM SUB SERCH I BY SID(I)=ID
1000 L=0:R=NUM
1010 IF L=R THEN PRINT "NOT FOUND":X=0:GOTO 1090
1020 X=INT ((L+R)/2)
1030 IF SID(X)=ID THEN GOTO 1090
1040 IF SID(X)<ID THEN L=X+1
1050 IF SID(X)>ID THEN R=X
1060 LOCATE 0,2:PRINT "X=";X;", SID(X)=";SID(X)
1070 LOCATE 0,3:PRINT L;" ";R
1080 GOTO 1020
1090 LOCATE 0,0:PRINT "ID=";ID;", SID(X)=";SID(X):INPUT K$
1100 RETURN
1110 REM 
1120 REM READ ATTEND.DAT
1130 OPEN "F:ATTEND.DAT"FOR INPUT AS #2
1140 CLS
1150 FOR I=1 TO NUM
1160 LOCATE 0,0:PRINT I;"/";NUM
1170 INPUT #2,ID,P$
1180 LOCATE 0,1:PRINT ID;" ";P$
1190 IF SID(I)=ID THEN PRS$(I)=P$:GOTO 1210
1200 GOSUB 990:IF SID(X)=ID THEN PRS$(X)=P$
1210 NEXT I
1220 CLOSE #2
1230 RETURN
1240 REM 
1250 REM SUB SET DATE NUM TO DTE
1260 CLS
1270 RESTORE 1410
1280 FOR I=0 TO 16
1290 READ D,X
1300 IF D=0 THEN GOTO 1340
1310 DT(X)=D
1320 LOCATE (X-1-INT ((X-1)/8)*8)*5,INT ((X-1)/8)+1:PRINT DT(X);" ";
1330 NEXT
1340 LOCATE 0,0:INPUT "DAY (MMDD)?",K$:MD=VAL (K$)
1350 RESTORE 1410
1360 FOR I=1 TO 16
1370 IF DT(I)=0 THEN GOTO 1400
1380 IF DT(I)=MD THEN DTE=I:LOCATE 0,0:WAIT 100: PRINT "SELECTED ";DT(I);" ";DTE;:WAIT 0:GOTO 1400
1390 NEXT I
1400 RETURN
1410 DATA 0613,1,0615,2
1420 DATA 0620,3,0622,4
1430 DATA 0627,5,0629,6
1440 DATA 0704,7,0706,8
1450 DATA 0711,9,0713,10
1460 DATA 0718,11,0720,12
1470 DATA 0725,13,0727,14
1480 DATA 0801,15,0803,16
1490 DATA 0,0
