;
;	MP/M 2 LDRBIOS for z80pack machines using SD-FDC
;
;	Copyright (C) 1989-2024 by Udo Munk
;	Copyright (C) 2025 by Thomas Eberhardt
;
; History:
; 12-MAR-2025 first public release based on cpmsim/srcmpm and RP2xxx/srccpm3
;
	ORG	1700H
;
;	I/O ports
;
CONSTA	EQU	0		;console status port
CONDAT	EQU	1		;console data port
FDC	EQU	4		;FDC
;
;	jump vector for individual subroutines
;
	JP	BOOT		;perform cold start initialization
	JP	WBOOT		;perform warm start initialization
	JP	CONST		;check for console input char ready
	JP	CONIN		;read console character in
	JP	CONOUT		;write console character out
	JP	LIST		;write list character out
	JP	AUXOUT		;write auxiliary output char
	JP	AUXIN		;read auxiliary input char
	JP	HOME		;move head to track 0 on selcted disk
	JP	SELDSK		;select disk drive
	JP	SETTRK		;set track number
	JP	SETSEC		;set sector number
	JP	SETDMA		;set dma address
	JP	READ		;read specified sector
	JP	WRITE		;write specified sector
	JP	LISTST		;return list status
	JP	SECTRAN		;translate logical to physical sector
;
;	fixed data tables for a	IBM-compatible 8" SD disk
;
;	disk parameter header
;
DPH:	DEFW	TRANS,0000H
	DEFW	0000H,0000H
	DEFW	DIRBF,DPBLK
	DEFW	CHK00,ALL00
;
;	sector translate vector for the IBM 8" SD disk
;
TRANS:	DEFB	1,7,13,19	;sectors 1,2,3,4
	DEFB	25,5,11,17	;sectors 5,6,7,8
	DEFB	23,3,9,15	;sectors 9,10,11,12
	DEFB	21,2,8,14	;sectors 13,14,15,16
	DEFB	20,26,6,12	;sectors 17,18,19,20
	DEFB	18,24,4,10	;sectors 21,22,23,24
	DEFB	16,22		;sectors 25,26
;
;	disk parameter block for the IBM 8" SD disk
;
DPBLK:	DEFW	26		;sectors per track
	DEFB	3		;block shift factor
	DEFB	7		;block mask
	DEFB	0		;extent mask
	DEFW	242		;disk size-1
	DEFW	63		;directory max
	DEFB	192		;alloc 0
	DEFB	0		;alloc 1
	DEFW	16		;check size
	DEFW	2		;track offset
;
;	signon message
;
SIGNON:	DEFB	13,10
	DEFM	'LDRBIOS V1.2 for Z80SIM, '
	DEFM	'Copyright 1989-2024 by Udo Munk'
	DEFM	' & 2025 by Thomas Eberhardt'
	DEFB	13,10,0
;
;	end of fixed tables
;
;	individual subroutines to perform each function
;
BOOT:	LD	HL,SIGNON	;print message
BOOTL:  LD	A,(HL)
	OR	A
	JP	Z,BOOTF
	OUT	(CONDAT),A
	INC	HL
	JP	BOOTL
BOOTF:	LD	A,10H		;setup FDC command
	OUT	(FDC),A
	LD	HL,CMD
	LD	A,L
	OUT	(FDC),A
	LD	A,H
	OUT	(FDC),A
	JP	WBOOT
;
;	those are not implemented in loader bios
;
WBOOT:
CONST:
CONIN:
LIST:
AUXOUT:
AUXIN:
WRITE:
LISTST:
	RET
;
;	console character output from register c
;
CONOUT: LD	A,C		;get to accumulator
	OUT	(CONDAT),A	;send character to console
	RET
;
;
;	i/o drivers for the disk follow
;
;	move to the track 00 position of current drive
;	translate this call into a settrk call with parameter 00
;
HOME:	LD	C,0		;select track 0
	JP	SETTRK		;we will move to 00 on first read/write
;
;	select disk given by register C
;
SELDSK:	PUSH	BC
	CALL	BOOT		;signon message
	POP	BC
	LD	HL,0000H	;error return code
	LD	A,C
	OR	A		;we boot from drive 0 only
	RET	NZ		;return error
;	disk number is in the proper range
;	return proper disk parameter header address
	LD	HL,DPH
	RET
;
;	set track given by register c
;
SETTRK: LD	A,C
	LD	(DDTRK),A
	RET
;
;	set sector given by register c
;
SETSEC: LD	A,C
	LD	(DDSEC),A
	RET
;
;	translate the sector given by BC using the
;	translate table given by DE
;
SECTRAN:
	EX	DE,HL		;hl=.trans
	ADD	HL,BC		;hl=.trans(sector)
	LD	L,(HL)		;l = trans(sector)
	LD	H,0		;hl= trans(sector)
	RET			;with value in hl
;
;	set dma address given by registers b and c
;
SETDMA: LD	A,C		;low order address
	LD	(DDLDMA),A
	LD	A,B		;high order address
	LD	(DDHDMA),A	;in dma
	RET
;
;	perform read operation
;
READ:	LD	A,20H		;read from drive 0
	OUT	(FDC),A		;ask FDC to execute the command
	IN	A,(FDC)		;get status from FDC
	RET
;
BEGDAT	EQU	$
DIRBF:	DEFS	128		;scratch directory area
ALL00:	DEFS	31		;allocation vector
CHK00:	DEFS	16		;check vector
;
;	FDC command bytes
;
CMD:	DS	4
DDTRK	EQU	CMD+0		;track
DDSEC	EQU	CMD+1		;sector
DDLDMA	EQU	CMD+2		;dma address low
DDHDMA	EQU	CMD+3		;dma address high
;
ENDDAT	EQU	$		;end
DATSIZ	EQU	$-BEGDAT	;size of data area
;
	END			;of bios
