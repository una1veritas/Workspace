100 REM ATTENDBK
110 DIM SID(100),NAM$(100)*12,PRS(100)
120 NUM=0: DTE=1
130 IF SID(1)=0 GOSUB 370
140 CLS
150 LOCATE 0,0:PRINT "R)READ PRES. M)SHOW (W)WRITE PRES. S)SET DATE"
160 K$=INKEY$
170 IF K$="R" GOSUB 720:GOTO 140
180 IF K$="M" GOSUB 230:GOTO 140
190 IF K$="W" GOSUB 520:GOTO 140
200 IF K$="S" GOSUB 1000:GOTO 140
210 GOTO 160
220 END
230 REM SUB. SHOW STUDENT LIST
240 CLS
250 I=1
260 LOCATE 0,0
270 PRINT USING "### ######## &&&&&&&&&&&&&&&&";I;SID(I);NAM$(I);PRS(I)
280 K$=INKEY$ :IF K$<>"" THEN 280
290 K$=INKEY$ :IF K$="" THEN 290
300 IF K$="N" AND I<NUM THEN I=I+1:GOTO 260
310 IF K$="B" AND I>1 THEN I=I-1:GOTO 260
320 IF K$="P" THEN PRS(I)=PRS(I) OR (&4):GOTO 260
330 IF K$="A" THEN PRS(I)=PRS(I) AND (NOT (&H4)):GOTO 260
340 IF K$="X" THEN RETURN
350 GOTO 290
360 RETURN
370 REM SUB. READ MEIBO.DAT
380 CLS
390 PRINT "READING MEIBO.DAT"
400 NUM=0
410 OPEN "F:MEIBO.DAT" FOR INPUT AS #1
420 INPUT #1,ID,CS$,NA$,GD$
430 IF ID=0 THEN GOTO 500
440 NUM=NUM+1
450 SID(NUM)=ID
460 NAM$(NUM)=NA$
470 LOCATE 0,1
480 PRINT USING "### ########";NUM;SID(NUM)
490 GOTO 420
500 CLOSE #1
510 RETURN
520 REM SUB. WRITE PRS
530 CLS :PRINT "WRITIG PRESENCE"
540 OPEN "F:PRESEN.DAT" FOR OUTPUT AS #2
550 FOR I=1 TO NUM
560 PRINT #2,SID(I);",";PRS(I)
570 NEXT I
580 CLOSE #2
590 RETURN
600 REM SUB SERCH I BY SID(I)=ID
610 L=0:R=NUM
620 IF L=R THEN PRINT "NOT FOUND":X=0:GOTO 700
630 X=INT ((L+R)/2)
640 IF SID(X)=ID THEN GOTO 700
650 IF SID(X)<ID THEN L=X+1
660 IF SID(X)>ID THEN R=X
670 LOCATE 0,2:PRINT "X=";X;", SID(X)=";SID(X)
680 LOCATE 0,3:PRINT L;" ";R
690 GOTO 630
700 LOCATE 0,0:PRINT "ID=";ID;", SID(X)=";SID(X):INPUT K$
710 RETURN
720 REM READ PRESENCE
730 OPEN "F:PRESEN.DAT"FOR INPUT AS #2
740 CLS
750 FOR I=1 TO NUM
760 LOCATE 0,0:PRINT I
770 INPUT #2,ID,P
780 IF SID(I)=ID THEN PRS(I)=P:GOTO 800
790 GOSUB 600:IF SID(X)=ID THEN PRS(X)=P
800 NEXT I
810 CLOSE #2
820 RETURN
1000 REM SUB SET DATE BIT TO DTE
1010 INPUT "DAY MMDD=";MD
1020 RESTORE 1200
1030 FOR I=1 TO 16
1040 READ D,X
1050 IF D=MD THEN DTE=X: PRINT "SELECTED ";MD;" ";DTE;" ";X: GOTO 1070
1060 NEXT I
1070 RETURN
1200 DATA 0613,2,0615,4
1210 DATA 0620,8,0622,&10
1220 DATA 0627,&20,0629,&40
1230 DATA 0704,&80,0706,&100
1240 DATA 0711,&200,0713,&400
1250 DATA 0718,&800,0720,&1000
1260 DATA 0725,&2000,0727,&4000
1270 DATA 0801,&8000,0803,&10000
