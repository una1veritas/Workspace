 30 NF=0:PN=11897
 50 DEF FNA(X)=10*INT(X/16)+X-16*INT(X/16)
 60 DIM NM$(64),T0%(64),T9%(64)
 70 AV=0
 80 DV=1:Y=1:X=PEEK(8994)
 82 IFX<=YTHEN85
 84 DV=DV+1 : Y=Y+Y : GOTO 82
 85 di=2
 86 PRINT!(21)"Directory Utilty":PRINT
 90 PRINT" 1 > Directory":PRINT" 2 > Sorted by Name":PRINT" 3 > Sorted by track"
 95 PRINT" 4 > Sector Directory"
 100 IFdi=2THENPOKE2888,0:POKE8722,0
 105 PRINT:INPUT"Choice <1>";#di,ch
 110 IFch=0THENch=1
 120 IFch<4THEN500
 125 F=VAL(Z$):T=DV:DV=2:GOTO140
 130 INPUT"First, last tracks";F,T
 140 IFF<1ORT>39ORT<FTHEN130
 145 PRINT!(21);:PRINT"OS-65D V3.3"
 150 PRINT"Sector Directory":PRINT"----------------";
 160 FORI=FTOT
 170 IFI<10THENDISK!"DI 0"+MID$(STR$(I),2):GOTO190
 180 DISK!"DI"+STR$(I)
 190 NEXTI:END
 500 PRINT!(21)"Output to which device:"
 510 PRINT"2 = video, 4 = printer"
 520 INPUT"<2>";#di,dv:IFdv=0THENdv=2
 10010 REM PRINT DIRECTORY
 10020 ts=ch-1
 10030 PRINT!(21);:PRINT#DV,"OS-65D Version 3.3      ";
 10035 PRINT#DV," -- Directory --"
 10040 PRINT#DV,"File    Tracks          File    Tracks"
 10060 DISK ! "CA 2E79=12,1
 10070 GOSUB 11000
 10080 DISK ! "CA 2E79=12,2
 10090 GOSUB 11000
 10093 IFTS=0THENGOTO10100
 10095 IFTS=1THENDEFFNB(X)=NM$(I)<NM$(L):GOSUB20000
 10097 IFTS=2THENDEFFNB(X)=T0%(I)<T0%(L):GOSUB20000
 10100 FORI=0TOAV-1STEP2
 10110 PRINT#dv,nm$(i)TAB(7)t0%(i)"-"t9%(i);
 10115 IFnm$(i+1)<>""THENPRINT#dv,TAB(24)nm$(i+1)t0%(i+1)"-"t9%(i+1)
 10120 NEXT I
 10130 PRINT#DV,64-AV"/ 64 Entries free"
 10140 INPUT"<RETURN>";a$
 10150 POKE 2888,27:POKE8722,27:END
 11000 REM
 11040 FOR I=PN TO PN+248 STEP 8
 11050 IF PEEK(I)=35 THEN 11130
 11060 N$=CHR$(PEEK(I))+CHR$(PEEK(I+1))+CHR$(PEEK(I+2))+CHR$(PEEK(I+3))
 11070 NM$(AV)=N$+CHR$(PEEK(I+4))+CHR$(PEEK(I+5))+" "
 11080 T0%(AV)=FNA(PEEK(I+6)):T9%(AV)=FNA(PEEK(I+7))
 11120 AV=AV+1
 11130 NEXT I
 11140 RETURN
 20000 REM SHELL SORT
 20022 M=AV-1
 20025 M=INT(M/2)
 20030 IF M=0 THEN RETURN
 20032 J=0 : K=AV-1-M
 20040 I=J
 20050 L=I+M
 20070 IFFNB(0)THEN20120
 20080 T$=NM$(I):NM$(I)=NM$(L):NM$(L)=T$
 20090 T%=T0%(I):T0%(I)=T0%(L):T0%(L)=T%
 20100 T%=T9%(I):T9%(I)=T9%(L):T9%(L)=T%
 20105 I=I-M
 20110 IF I>=0 THEN 20050
 20120 J=J+1
 20130 IF J>K THEN 20025
 20140 GOTO 20040
