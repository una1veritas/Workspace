 10 REM              OS-65BD LEVEL I
 20 REM        By R. Whitesel 8/80 BEDFRD HTS
 30 REM      (C) 1980 By Ohio Scientific, Inc.
 40 REM            All rights reserved
 50 REM
 60 REM
 100 REM BASIC EXECUTIVE
 115 Q$=CHR$(34):POKE14172,8:POKE14170,16
 120 REM SETUP INFLAG & OUFLAG FROM DEFAUL
 130 X=PEEK(10950): POKE 8993,X: POKE 8994,X: POKE 2888,0
 135 TW=79: IF X=2 THEN TW=63: IF PEEK(9636)<>64 THEN TW=21
 140 IFPEEK(57088)=223THENPOKE9794,37
 150 DIM NA$(64): REM HOLDS FILE NAMES
 160 REM KILL NEW & LIST
 170 POKE 741,10: POKE 750,10
 180 REM KILL (C)
 190 POKE 2073,96
 200 REM PRINT MENU AND GET USER REQUEST
 210 REM
 215 PRINT!(21)
 220 GOSUB 61000: PRINT "Working ";: GOSUB 30000: GOSUB 61040
 224 REM
 225 PRINT"OS-65D Tutorial disk three"
 226 PRINT"   September 16, 1981"
 230 TB=10: FOR X=2 TO 9
 240 PRINT TAB(TB);X-1;"> LOAD program called ";Q$;NA$(X);
 250 PR$=NA$(X):GOSUB 62000:PRINT Q$:NEXT X
 260 PRINT TAB(TB);X-1;"> Rename a program file"
 265 INPUT"Type number of selection and press RETURN ";QA$
 270 IF LEN(QA$) <> 1THEN GOSUB 61000: GOTO 224
 280 SE=VAL(QA$): IF SE < 1 THEN GOSUB61000:GOTO 224
 290 IF SE < 9 THEN SEL$=NA$(SE+1): GOTO 60000
 300 GOSUB 20020: RUN
 20000 REM RENAME FILE UTILITY UNDER OS-65D VERSION 3.0
 20010 REM
 20020 FLAG=0 : PN=11897: ER=0: REM CLR ERROR FLG
 20030 GOSUB61000: INPUT "Old file name ";A$: PRINT
 20040 IF LEN(A$)>6 THEN 20030
 20050 IF LEN(A$)<6 THEN A$=A$+" " : GOTO 20050
 20060 DISK!"CA 2E79=12,1
 20070 GOSUB20120:IFFLTHENDISK!"SA 12,1=2E79/1":RETURN
 20080 DISK!"CA 2E79=12,2
 20090 GOSUB20120:IFFLTHENDISK!"SA 12,2=2E79/1":RETURN
 20100 PRINT Q$;A$;:PR$=A$:GOSUB 62000
 20105 PRINT Q$;" not found on diskette."
 20110 GOSUB 61010: GOSUB 61000: ER=1: RETURN
 20120 REM
 20130 REM SEE IF FILE NAME A$ IS IN DIRECTORY BUFFER
 20140 REM
 20150 FOR I=PNT TO PN+248 STEP 8
 20160 FOR J=I TO I+5
 20170 IF CHR$(PEEK(J))<>MID$(A$,J-I+1,1) THEN 20250
 20180 NEXT J
 20190 GOSUB61000: PRINT "Rename " ;Q$;A$;
 20192 PR$=A$:GOSUB 62000:PRINT Q$;
 20195 INPUT " to ";AN$: PRINT
 20200 IF LEN(AN$)>6 THEN 20190
 20210 IF LEN(AN$)<6 THEN AN$=AN$+" " : GOTO 20210
 20220 IF MID$(AN$,1,1)<"A" OR MID$(AN$,1,1)>"Z" THEN 20190
 20225 FOR X=2 TO 9: IF NA$(X) <> AN$ GOTO 20228
 20226 GOSUB 61000: PRINT "The name ";Q$;AN$;:PR$=AN$:GOSUB 62000
 20227 PRINT Q$;" is already used .": GOSUB 61010: RUN
 20228 NEXT X
 20230 FOR J=I TO I+5 : POKE J,ASC(MID$(AN$,J-I+1,1)) : NEXT J
 20240 FLAG=1 : RETURN
 20250 NEXT I
 20260 FLAG=0 : RETURN
 30000 REM DIRECTORY UTILITY FOR OS-65D VERSION 3.2  NMHZ
 30010 REM
 30030 PN=11897: NF=0: NC=0
 30060 IF X=<Y THEN 30180
 30180 DISK!"CA 2E79=12,1
 30190 GOSUB 30240
 30200 DISK!"CA 2E79=12,2
 30210 GOSUB 30240
 30230 RETURN
 30240 REM
 30250 REM READ DIRECTORY OUT OF BUFFER INTO ARRAYS
 30260 REM
 30270 FOR I=PN TO PN+248 STEP 8: GOSUB 61020
 30280 IF PEEK(I)=35 THEN NF=NF+1 : GOTO 30350
 30290 N$=""
 30300 FOR J=I TO I+5: GOSUB 61020
 30310 N$=N$+CHR$(PEEK(J))
 30320 NEXT J
 30330 NA$(NC)=N$: NC=NC+1: REM SAVE NAME
 30350 NEXT I
 30360 RETURN
 60000 REM UNLOCK SYSTEM
 60001 REM
 60010 REM
 60020 REM REPLACE "NEW" AND "LIST"
 60030 POKE 741,76 : POKE 750,78
 60040 REM
 60050 REM ENABLE CONTROL-C
 60060 POKE 2073,173
 60070 REM
 60080 REM DISABLE "REDO FROM START" & <CR> ON INPUT
 60090 POKE 2893,55 : POKE 2894,8: POKE 2888,27
 60100 GOSUB 61000
 60110 REM
 60120 PRINT"The program ";Q$;SEL$;:PR$=SEL$:GOSUB62000
 60125 PRINT Q$;" has been loaded."
 60130 PRINT
 60140 PRINT"To SAVE your program type:"
 60150 PRINTTAB(20);"DISK!";Q$;"PUT ";SEL$;:PR$=SEL$:GOSUB 62000
 60152 PRINT Q$;"   <CR>"
 60155 PRINT"To RUN your program type:";
 60156 PRINTTAB(34);"RUN  <CR>"
 60160 PRINT"To LIST the program type:";
 60161 PRINTTAB(34);"LIST <CR>"
 60163 PRINT"To clear the workspace type:";
 60164 PRINTTAB(34);"NEW  <CR>"
 60165 PRINT"To return to the menu type:";
 60170 PRINTTAB(29);"RUN";CHR$(34);"MENU";CHR$(34);" <CR>"
 60200 DISK!"MEM D000,D000"
 60210 PRINT#5,"DISK!"CHR$(34)"LO "+SEL$
 60220 PRINT#5,"DISK!"CHR$(34)"IO 02,02"
 60230 DISK!"IO 10,00
 60300 END
 61000 PRINT !(28);:RETURN
 61010 PRINT: INPUT "Hit <CR> to continue ";QA$: PRINT: RETURN
 61020 PRINT ".";: IF POS(X)>=TW THEN PRINT CHR$(13);
 61030 RETURN
 61040 PRINT CHR$(13);SPC(TW);CHR$(13);: RETURN
 62000 NU=1
 62010 IF ASC(RIGHT$(PR$,NU))=32 THEN NU=NU+1:GOTO 62010
 62020 IF NU=1 THEN RETURN
 62030 FOR BS=1 TO NU-1
 62040 PRINT CHR$(8);
 62050 NEXT BS
 62060 RETURN
