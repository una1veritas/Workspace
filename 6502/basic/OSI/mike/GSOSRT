 10 REM OS65D V3.1 GENERALIZED SORT UTILITY
 15 REM            9/21/79  REV 8/26/81
 20 REM
 25 FORI=1TO5:PRINT:NEXT:BY=2048
 30 DEF FNA(X)=NB*X+1
 35 PRINT"** OS-65D V3.2 GENERALIZED SORT UTILITY **"
 40 PRINT:PRINT
 45 INPUT"ENTER DATA FILE NAME";A$:T=LEN(A$):IFT>0THENT=ASC(A$)
 50 IFT<65ORT>90GOTO45
 55 FI$=A$:PRINT:PRINT
 60 INPUT"IS THIS FILE A MDMS MASTER FILE";M$:PRINT
 65 M$=LEFT$(M$,1):IFM$<>"Y"GOTO110
 70 FI$=FI$+"     ":FI$=LEFT$(FI$,5)+"0"
 75 DISK OPEN,6,FI$:POKE12076,6:POKE12042,BY/64
 80 INPUT#6,A$,NB,NF,PH,EN:IFEN=1GOTO49999
 85 EN=EN-1:DISK GET,1
 90 PRINT:PRINT"SORT ON WHICH OF THE FOLLOWING FIELDS:"
 95 FORI=1TONF:INPUT#6,A$,B$:PRINTI">  ";A$:NEXT
 100 INPUT"WHICH ONE";SE:IFSE<1ORSE>NFGOTO100
 105 PRINT:GOTO175
 110 INPUT"IS THIS FILE 'RANDOM' OR 'SEQUENTIAL'";A$
 115 DISK OPEN,6,FI$
 120 R$="R":IFLEFT$(A$,1)="S"THENR$="S":GOTO145
 125 INPUT"NUMBER OF BYTES PER RECORD";A
 130 IFA<1ORA>255GOTO125
 135 B=BY/A:IFB<0ORB>255GOTO125
 137 B1=INT(LOG(A)/LOG(2)+.1)
 140 POKE12076,B1:POKE12042,B
 145 INPUT"NUMBER OF FIELDS PER RECORD";NF
 150 IFNF<1ORNF>100GOTO145
 155 IFNF=1THENSE=1:GOTO170
 160 INPUT"SORT ON WHICH FIELD";SE
 165 IFSE<1ORSE>NFGOTO160
 170 INPUT"HOW MANY RECORDS TO SORT";EN:IFEN<1GOTO170
 175 PRINT:PRINT:PRINT"DESCRIPTION CONFIRMATION:":PRINT
 180 A$="NON-MDMS":IFM$="Y"THENA$="MDMS MASTER"
 185 PRINT"FILE TYPE"TAB(18)":  ";A$
 190 IFM$="Y"GOTO210
 195 A$="RANDOM":IFR$="S"THENA$="SEQUENTIAL"
 200 PRINT"DATA TYPE"TAB(18)":  "A$
 205 IFR$<>"S"THENPRINT"BYTES PER RECORD"TAB(18)": ";A
 210 PRINT"NUMBER OF FIELDS"TAB(18)": "NF
 215 PRINT"SORT ON FIELD"TAB(18)": "SE
 220 PRINT"RECORDS TO SORT"TAB(18)": "EN
 225 PRINT:INPUT"IS THIS CORRECT";Y$
 230 IFLEFT$(Y$,1)="N"THENRUN
 235 DIMR(EN),L$(EN),S$(EN,NF)
 240 PRINT:PRINT:PRINT"SORTING---"
 245 FORI=1TOEN:IFM$="Y"THEN DISK GET,FNA(I):GOTO255
 250 IFR$="R"THEN DISK GET,I-1
 255 FORJ=1TONF:INPUT#6,S$(I,J):NEXTJ
 260 L$(I)=S$(I,SE):R(I)=I:NEXTI
 265 T=0:FORI=1TOEN:IFVAL(L$(I))<>0THENT=T+1
 270 NEXT:A$="":B$=" ":IFT<EN/3GOTO285
 275 FORI=1TOEN:L$(I)=STR$(INT(VAL(L$(I))*1000)):NEXT
 280 A$=" ":B$=""
 285 SL=0:FORI=1TOEN:A=LEN(L$(I)):IFA>SLTHENSL=A
 290 NEXT:FORI=1TOEN:C$=L$(I)
 295 IFLEN(C$)<SLTHENC$=A$+C$+B$:GOTO295
 300 L$(I)=C$:NEXT
 305 FORI=2TOEN:IFL$(I-1)<=L$(I)GOTO330
 310 A$=L$(I):J=I:R0=R(I)
 315 J=J-1:IFJ<1ORL$(J)<=A$GOTO325
 320 L$(J+1)=L$(J):R(J+1)=R(J):GOTO315
 325 J=J+1:L$(J)=A$:R(J)=R0
 330 NEXT:PRINT:PRINT:PRINT"SORT COMPLETE.  WOULD YOU LIKE THE"
 335 PRINT"DATA TO BE STORED IN 'ASCENDING' OR"
 340 INPUT"'DESCENDING' ORDER";Y$
 350 IFR$="S"THENDISKOPEN,6,FI$
 355 FORI=1TOEN:IFM$="Y"THEN DISK GET,FNA(I):GOTO362
 360 IFR$="R"THENDISK GET,I-1
 362 X=I:IFLEFT$(Y$,1)="D"THENX=EN-I+1
 365 FORJ=1TONF:PRINT#6,S$(R(X),J):NEXTJ
 370 IFR$="R"ORM$="Y"THEN DISK PUT
 375 NEXTI:IFR$="S"THEN DISK PUT
 49999 PRINT:PRINT"OPERATIONS COMPLETE.":END
