 5 POKE133,122:CLEAR:POKE14172,8:POKE14170,16
 10 POKE2888,0:POKE8722,0
 20 X=PEEK(10950):POKE 8993,X:POKE8994,X:DIMAL%(76)
 30 IFPEEK(57088)=223THENPOKE9794,37
 40 DEFFNA(X)=10*INT(X/16)+X-16*INT(X/16)
 50 DEFFNB(X)=16*INT(X/10)+X-10*INT(X/10)
 100 GOSUB50000
 110 PRINT:PRINT"OS-65D Version 3.3 BASIC Executive"
 120 PRINT" 1 > Directory"
 130 PRINT" 2 > Create a new file"
 140 PRINT" 3 > Change a file name"
 150 PRINT" 4 > Delete file from diskette"
 160 PRINT" 5 > Create blank data diskette"
 170 PRINT" 6 > Create data diskette with files"
 180 PRINT" 7 > Create buffer space for data files"
 190 PRINT" 8 > Single or dual disk drive copier"
 200 PRINT" 9 > Enter OS-65D system"
 900 PRINT"Type the number of your selection "
 910 INPUT"and press RETURN ";S$:IFS$="PASS"ORS$="9"THEN60000
 915 IFLEN(S$)>1THENRUN
 920 S=INT(VAL(S$)):IFS<1ORS>8THENRUN
 980 GOSUB50010
 989 PRINT"     ";
 990 ONSGOSUB1000,2000,3000,4000,5000,5000,7000,8000
 998 IFP$="PASS"THEN60000
 999 GOTO100
 1000 PRINT"Directory utility":PRINT:PRINT:PRINT"Directory of";
 1005 GOSUB10000:GOSUB50010:GOSUB10100:GOSUB50010
 1010 PRINT#DV,"   -- Directory --":PRINT#DV
 1020 PRINT#DV,"File name   Track range"
 1030 PRINT#DV,"-----------------------":GOSUB20000
 1040 PRINT#DV:PRINT#DV,NF;"Entries free out of 64"
 1050 PRINT:GOTO10200
 2000 PRINT"Create utility":PRINT:PRINT
 2005 PRINT"Create a file on";:GOSUB10000
 2010 GOSUB50010:PRINT"Type in the name of the file you ";
 2015 PRINT"want to create and":PRINT
 2016 INPUT"depress RETURN (1-6 Characters) ";A$:IFLEN(A$)>6THEN2010
 2018 IFA$=""THENRUN
 2020 IFLEN(A$)<6THENA$=A$+" ":GOTO2020
 2030 IFMID$(A$,1,1)<"A"ORMID$(A$,1,1)>"Z"THEN2010
 2040 S=5:GOSUB20000:IFE$="Y"THEN40100
 2045 IFNF=0THEN40200
 2056 REM DELETED
 2060 REM DELETED
 2070 GOSUB50010:PRINT"Type in the number of ";
 2075 PRINT"tracks in this file":PRINT
 2076 INPUT"and press RETURN (1 to 68 )";N
 2080 IFN<1ORN>68THEN2070
 2090 FORT1=1TO76-N+1:FORTS=0TON-1
 2095 IFAL%(T1+TS)THEN2110
 2100 NEXTTS
 2102 PRINT:PRINT"** TRACKS AVAILABLE FOR ";A$;" **"
 2103 T2=T1+N-1:N=0
 2104 PRINT:PRINT:GOTO2120
 2110 NEXT T1
 2112 PRINT:PRINT"** NO ROOM FOR ";A$;" **"
 2114 PRINT:GOTO10200
 2120 PRINT"Do you want to initialize ":PRINT
 2121 INPUT"these tracks (Yes or No) <Yes> ";B$
 2123 IFLEFT$(B$,1)="N"THEN2200
 2125 GOSUB50010:PG=12:P$="123456789ABCDEF"
 2130 INPUT"How many pages per track (1 to 12) <12> ";PG
 2135 IFPG=0THENPG=12
 2140 IFPG<1ORPG>12THEN2125
 2150 P$=MID$(P$,PG,1)
 2152 PRINT"Initializing:"
 2153 FORI=T1TOT2:T$=RIGHT$(STR$(I+100),2)
 2155 PRINT"      Track ";T$
 2156 J$="3A7E":IFPEEK(8999)=49THENJ$="317E"
 2160 DISK!"IN "+T$
 2162 POKE10304,169:POKE10305,32:POKE10549,201:POKE10550,32
 2164 DISK!"SA "+T$+",1=D000/"+P$
 2166 POKE 10304,177:POKE10305,254:POKE10549,209:POKE10550,254
 2167 NEXT
 2170 IFS=6THENRETURN
 2200 S=2:GOTO20000
 3000 PRINT"Rename utility":PRINT:PRINT
 3005 PRINT"Rename a file on";:GOSUB10000
 3010 GOSUB50000:PRINT"Type in the name of the file that you";
 3015 PRINT" want to rename":PRINT
 3016 INPUT"and depress RETURN (1-6 Characters) ";A$
 3017 IFLEN(A$)>6THEN3010
 3018 IFA$=""THENRUN
 3020 IFLEN(A$)<6THENA$=A$+" ":GOTO3020
 3030 S=5:GOSUB20000:IFE$="N"THEN40000
 3035 O$=A$:PRINT
 3040 GOSUB50000:PRINT"Type in the name that will replace ";
 3044 PRINTCHR$(34);O$;CHR$(34)" in the directory":PRINT
 3045 INPUT"and depress RETURN (1-6 Characters) ";A$
 3047 IFLEN(A$)>6THEN3040
 3050 IFLEN(A$)<6THENA$=A$+" ":GOTO3050
 3055 IFLEFT$(A$,1)<"A"ORLEFT$(A$,1)>"Z"THEN3040
 3056 IFO$=A$THEN40100
 3060 GOSUB20000:IFE$="Y"THEN40100
 3070 S=3:GOTO20000
 4000 PRINT"Delete utility":PRINT:PRINT
 4005 PRINT"Delete a file on";:GOSUB10000
 4010 GOSUB50000:PRINT"Type in the name of the file that you";
 4015 PRINT" want to delete":PRINT
 4016 INPUT"and depress RETURN (1-6 Characters) ";A$
 4017 IFLEN(A$)>6THEN4010
 4018 IFA$=""THENRUN
 4020 IFLEN(A$)<6THENA$=A$+" ":GOTO4020
 4030 S=5:GOSUB20000:IFE$="N"THEN40000
 4040 S=4:GOTO20000
 5000 PRINT"Data disk create utility":PRINT:PRINT
 5010 PRINT"Be sure the tutorial disk is in drive A":PRINT
 5020 GOSUB10200:DISK!"SE A":DISK!"CA 7C00=07,2"
 5030 DISK!"CA 7D00=07,3":DISK!"CA 7E00=07,4":DISK!"CA 7F00=07,5"
 5033 IFS=6THENDISK!"CA 7C00=07,6"
 5035 GOSUB5100:DISK!"GO 2768":DISK!"SA 08,1=7C00/1"
 5040 DISK!"SA 08,2=7D00/1":DISK!"SA 08,3=7E00/1"
 5050 DISK!"SA 08,4=7F00/1":IFS=6THENGOSUB6000
 5070 GOSUB50010
 5080 PRINT"Your diskette is now ready for data files.":PRINT
 5090 PRINT:GOTO5505
 5100 GOSUB50010
 5105 PRINT"Remove your tutorial diskette from drive A and":PRINT
 5110 PRINT"replace it with your blank diskette.":PRINT:GOTO10200
 5500 GOSUB50010
 5505 PRINT"Remove your blank diskette from drive A and":PRINT
 5510 PRINT"replace it with your tutorial diskette.":PRINT:GOTO10200
 6000 P$="12":T1=1:T2=5:GOSUB2153
 6010 T1=9:T2=28:GOTO2153
 7000 L=12670:IFPEEK(8999)=58THENL=14974
 7005 PRINT"Buffer set utility":PRINT:PRINT
 7010 PRINT"Type in the number of file buffers";
 7015 PRINT" you need":PRINT
 7020 INPUT"and depress RETURN (0,1, or 2) <0>";N
 7030 IFN<0ORN>2THENGOSUB50010:GOTO7010
 7040 L=L+N*3072:N=N+1:GOSUB50010:ONNGOTO7100,7200,7300
 7100 PRINT"No file buffers are resident.":GOTO7500
 7200 PRINT"A single buffer is now resident.":GOTO7500
 7300 PRINT"Two buffers are now resident."
 7500 PRINT
 7510 PRINT"Type in your program and save it on your diskette."
 7520 GOSUB59000
 7540 POKE120,L+1-INT((L+1)/256)*256:POKE121,INT((L+1)/256):POKEL,0:NEW
 8000 X=PEEK(8960):POKE133,X:RUN"COPIER
 10000 PRINT" which drive ?":PRINT
 10004 INPUT"Type A,B,C or D and depress RETURN <A> ";D$
 10005 IFD$=""THEND$="A"
 10010 IFD$<"A"ORD$>"D"ORLEN(D$)<>1THENGOSUB50000:GOTO10004
 10020 DISK!"SE "+D$:RETURN
 10100 PRINT"Do you want to list the directory":PRINT
 10105 INPUT"to the printer (Yes or No) <No> ";P$
 10110 DV=2:IFLEFT$(P$,1)="Y"THENDV=1
 10120 RETURN
 10200 INPUT"Depress RETURN to continue ";P$:RETURN
 20000 NF=0:E$="N":FORK=0TO76:AL%(K)=0:NEXT
 20003 IFS=5THENPRINT:PRINT:PRINT"Please wait.";
 20005 DISK!"CA 2E79=08,1":GOSUB 20100:DISK!"SA 08,1=2E79/1"
 20006 IFS>1ANDS<5THEN DISK!"SA 08,1=2E79/1
 20008 IF S>1 AND E$="Y" THENRETURN
 20010 DISK!"CA 2E79=08,2":GOSUB20100:DISK!"SA 08,2=2E79/1"
 20015 IFS>1ANDS<5THEN DISK!"SA 08,2=2E79/1
 20020 RETURN
 20100 FORI=11897TO12145STEP8
 20110 ONSGOSUB20200,30000,20300,20400,30000
 20115 IFNF=1ANDS=2THENGOSUB30300:E$="Y":RETURN
 20120 NEXT:RETURN
 20200 IFPEEK(I)=35THENNF=NF+1:RETURN
 20210 GOSUB30100
 20220 PRINT#DV,N$;TAB(12);FNA(PEEK(I+6));TAB(16);"-";
 20230 PRINT#DV,TAB(17);FNA(PEEK(I+7)):RETURN
 20300 GOSUB30100:IFO$<>N$THENRETURN
 20310 E$="Y":GOTO30200
 20400 GOSUB30100:IFA$<>N$THENRETURN
 20410 E$="Y":A$="######":GOSUB30200:POKEI+6,0:POKEI+7,0:RETURN
 30000 IFPEEK(I)=35THENNF=NF+1:RETURN
 30005 GOSUB30100:IFN$=A$THENE$="Y"
 30010 T0=FNA(PEEK(I+6)):T9=FNA(PEEK(I+7))
 30020 FORK=T0TOT9:AL%(K)=-1:NEXT:RETURN
 30100 N$="":FORJ=ITOI+5:N$=N$+CHR$(PEEK(J)):NEXT
 30110 PRINT" ";:RETURN
 30200 FORJ=1TO6:POKEI+J-1,ASC(MID$(A$,J,1)):NEXT:RETURN
 30300 GOSUB30200:POKEI+6,FNB(T1):POKEI+7,FNB(T2):NF=255:RETURN
 40000 PRINT:PRINT:PRINT"** "CHR$(34);A$;CHR$(34)" was not found ";
 40010 PRINT"in the directory. **":PRINT:GOTO10200
 40100 PRINT:PRINT:PRINT"** "CHR$(34);A$;CHR$(34)" already exists ";
 40110 PRINT"in the directory. **":PRINT:GOTO10200
 40200 PRINT:PRINT:PRINT"** Directory full **":PRINT:GOTO10200
 50000 ST=11984:FORII=0TO36:READSC:POKEST+II,SC:NEXT:RESTORE
 50010 IFPEEK(8999)=58THEN PRINTCHR$(27);CHR$(21):RETURN
 50015 POKE8955,208
 50020 POKE8956,46:X=USR(X):RETURN
 50030 DATA 169,208,141,219,46,169,32,162,0,157,0,208,232
 50040 DATA 208,250,172,219,46,200,140,219,46,192,232,240,10
 50050 DATA 192,216,208,235,160,224,169,14,208,239,96
 59000 POKE741,76:POKE750,78:POKE2073,173:POKE2893,55:POKE2894,8
 59010 POKE2888,27:X=PEEK(8960):POKE133,X
 59015 IFPEEK(65514)=254THENPOKE11292,76:POKE11293,0:REM RE M CMD CEGMON.
 59020 RETURN
 60000 GOSUB59000
 60010 GOSUB50000:CLEAR
 60020 PRINT"The system is now open for modification."
