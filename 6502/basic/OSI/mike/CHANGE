 1 PRINTCHR$(27);CHR$(28);
 10 REM CHANGE PARAMETER UTILITY UNDER OS-65D VERSION 3.3
 20 REM *R2 1/4/2011
 25 XZ=14974:IFPEEK(8999)=49THENXZ=12670
 30 PRINT : PRINT "CHANGE PARAMETER UTILITY" : PRINT
 36 PRINT : INPUT "CHANGE BASIC'S WORKSPACE LIMITS (Y/N) ";A$
 37 PRINT:IF A$<>"Y" THEN END
 40 PRINT"HOW MANY 12 PAGE BUFFERS DO YOU WANT "
 41 INPUT"BEFORE THE WORKSPACE (0, 1, or 2) ";B
 45 PRINT
 50 IFB<0ORB>2ORB<>INT(B)THENPRINT"ANSWER 0, 1, OR 2":PRINT:GOTO40
 60 L=XZ+B*3072: REM L=$327E PLUS B*$C00
 70 IF B<>0 THEN 130
 80 INPUT "WANT TO LEAVE ANY ROOM BEFORE THE WORKSPACE (Y/N) ";A$
 90 IF MID$(A$,1,1)<>"Y" THEN 170
 100 PRINT:INPUT "HOW MANY BYTES ";B
 110 L=L+B
 120 GOTO 170
 130 INPUT "WANT TO LEAVE ANY ADDITIONAL ROOM (Y/N) ";A$
 140 IF MID$(A$,1,1)<>"Y" THEN 170
 150 PRINT:INPUT "HOW MANY BYTES ";B
 160 L=L+B
 170 PRINT:PRINT "THE BASIC WORKSPACE WILL BE SET TO START AT";L
 180 PRINT "LEAVING";L-XZ;"BYTES FREE IN FRONT OF THE WORKSPACE"
 190 INPUT "IS THAT ALRIGHT (Y/N) ";A$
 200 IF MID$(A$,1,1)="Y" THEN 210
 204 PRINT:INPUT "NEW LOWER LIMIT ";L:PRINT:IF L<XZ THEN 204
 206 PRINT L-XZ;"BYTES WILL BE FREE BEFORE THE WORKSPACE"
 210 MP=PEEK(8960)
 220 PRINT:PRINT "YOU HAVE";(MP+1)/4;"K OF RAM":PRINT
 230 U=(MP+1)*256
 240 INPUT "DO YOU WANT TO LEAVE ANY ROOM AT THE TOP (Y/N) ";A$
 250 IF MID$(A$,1,1)<>"Y" THEN 280
 260 PRINT:INPUT "HOW MANY BYTES ";B
 270 U=U-B
 280 PRINT:PRINT "THE BASIC WORKSPACE WILL BE SET TO END AT";U
 290 PRINT "LEAVING";(MP+1)*256-U;"BYTES FREE AFTER THE WORKSPACE"
 300 INPUT "IS THAT ALRIGHT (Y/N) ";A$
 310 PRINT:IF MID$(A$,1,1)="Y" THEN 320
 312 INPUT"NEW UPPER LIMIT";U:PRINT:IFU>49152ORU<L+3THEN312
 314 PRINT (MP+1)*256-U;"BYTES WILL BE FREE AFTER THE WORKSPACE"
 320 PRINT:PRINT"YOU WILL HAVE";U-L+1;"BYTES FREE IN THE WORKSPACE"
 330 INPUT "IS THAT ALRIGHT (Y/N) ";A$
 340 IF MID$(A$,1,1)<>"Y" THEN RUN
 350 REM
 360 REM NOW DO THE ADJUSTMENTS
 370 REM
 380 POKE132,U-INT(U/256)*256:POKE133,INT(U/256)
 390 POKE120,L+1-INT((L+1)/256)*256:POKE121,INT((L+1)/256):POKEL,0:NEW
 1000 REM
 1010 REM
 1020 PRINT "THE TERMINAL WIDTH IS SET FOR";PEEK(23)
 1030 INPUT "DO YOU WANT TO CHANGE IT (Y/N)";A$
 1040 IF A$<>"Y" THEN 1100
 1050 INPUT "NEW VALUE";WD
 1060 IF WD<14 OR WD>255 THEN PRINT "BAD VALUE" : GOTO 1050
 1070 POKE 23,WD
 1080 NC=INT(WD/14)*14: REM *R1
 1090 POKE 24,NC
 1100 PRINT : PRINT "BASIC & THE ASSEMBLER USE";(PEEK(8960)+1)/4;
 1110 PRINT"K OF MEMORY (TOTAL) (";PEEK(8960)+1;"PAGES )":REM*R1
 1120 INPUT "WOULD YOU LIKE TO CHANGE THIS (Y/N)";A$
 1130 IF A$<>"Y" THEN 1170
 1140 INPUT "HOW MANY PAGES SHOULD THEY USE (MIN: 52) ";KK
 1150 IF KK<52 GOTO 1140: REM *R1
 1152 IF KK>192 THEN PRINT"ABSOLUTE MAX IS 192":GOTO 1140:REM*R1
 1154 DD=PEEK(KK*256-1): POKE KK*256-1,256-DD
 1156 ND=PEEK(KK*256-1):POKE KK*256-1,DD: IF ND=256-DD GOTO 1160
 1158 PRINT "THIS COMPUTER DOESN'T HAVE";KK;"PAGES OF MEMORY": GOTO 1140
 1160 POKE 8960,KK-1: REM *R1
 1170 END
