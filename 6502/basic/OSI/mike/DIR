 1 PRINTCHR$(27);CHR$(28);
 10 POKE741,76:POKE750,78
 20 REM
 30 NF=0
 40 PN=11897
 50 DEF FNA(X)=10*INT(X/16)+X-16*INT(X/16)
 60 DIM NM$(64),T0%(64),T9%(64)
 70 AV=0
 80 DV=1 : Y=1 : X=PEEK(8994)
 82 IF X=<Y THEN 86
 84 DV=DV+1 : Y=Y+Y : GOTO 82
 86 PRINT:PRINT "DIRECTORY UTILITY":PRINT
 87 PRINT" 1> Directory":PRINT" 2> Directory sorted by name"
 88 PRINT" 3> Directory sorted by track":PRINT
 90 INPUT"Type 1, 2, or 3 and depress RETURN ";Z$
 91 IFZ$<>"1"ANDZ$<>"2"ANDZ$<>"3"THENRUN
 92 PRINT:IFZ$="2"THENZ$="N"
 93 IFZ$="3"THENZ$="T"
 95 PRINT "LIST ON LINEPRINTER INSTEAD OF DEVICE #";DV;
 96 INPUT A$ : IF MID$(A$,1,1)="Y" THEN DV=1
 100 IF Z$="N" OR Z$="T" THEN 10000
 110 PRINT "THEN IT WILL BE UNSORTED"
 10000 REM
 10010 REM
 10020 REM
 10030 PRINT #DV : PRINT #DV, "OS-65D VERSION 3.3"
 10035 PRINT #DV," -- DIRECTORY --" : PRINT #DV
 10040 PRINT #DV,"FILE NAME    TRACK RANGE"
 10050 PRINT #DV,"------------------------"
 10060 DISK ! "CA 2E79=08,1
 10070 GOSUB 11000
 10080 DISK ! "CA 2E79=08,2
 10090 GOSUB 11000
 10095 IF Z$="N" THEN GOSUB 20000
 10097 IF Z$="T" THEN GOSUB 21000
 10100 FOR I=0 TO AV-1
 10110 PRINT #DV,NM$(I);TAB(12);T0%(I);TAB(16);"-";TAB(17);T9%(I)
 10120 NEXT I
 10130 PRINT #DV : PRINT #DV,64-AV;"ENTRIES FREE OUT OF 64" : PRINT #DV
 10140 END
 11000 REM
 11010 REM
 11020 REM
 11040 FOR I=PN TO PN+248 STEP 8
 11050 IF PEEK(I)=35 THEN 11130
 11060 N$=CHR$(PEEK(I))+CHR$(PEEK(I+1))+CHR$(PEEK(I+2))+CHR$(PEEK(I+3))
 11070 NM$(AV)=N$+CHR$(PEEK(I+4))+CHR$(PEEK(I+5))
 11100 T0%(AV)=FNA(PEEK(I+6))
 11110 T9%(AV)=FNA(PEEK(I+7))
 11120 AV=AV+1
 11130 NEXT I
 11140 RETURN
 20000 REM
 20010 REM
 20015 REM
 20020 REM
 20022 M=AV-1
 20025 M=INT(M/2)
 20030 IF M=0 THEN RETURN
 20032 J=0 : K=AV-1-M
 20040 I=J
 20050 L=I+M
 20070 IF NM$(I)<NM$(L) THEN 20120
 20080 T$=NM$(I):NM$(I)=NM$(L):NM$(L)=T$
 20090 T%=T0%(I):T0%(I)=T0%(L):T0%(L)=T%
 20100 T%=T9%(I):T9%(I)=T9%(L):T9%(L)=T%
 20105 I=I-M
 20110 IF I>=0 THEN 20050
 20120 J=J+1
 20130 IF J>K THEN 20025
 20140 GOTO 20040
 21000 REM
 21010 REM
 21020 REM
 21022 M=AV-1
 21025 M=INT(M/2)
 21030 IF M=0 THEN RETURN
 21032 J=0 : K=AV-1-M
 21040 I=J
 21050 L=I+M
 21070 IF T0%(I)<T0%(L) THEN 21120
 21080 T$=NM$(I):NM$(I)=NM$(L):NM$(L)=T$
 21090 T%=T0%(I):T0%(I)=T0%(L):T0%(L)=T%
 21100 T%=T9%(I):T9%(I)=T9%(L):T9%(L)=T%
 21105 I=I-M
 21110 IF I>=0 THEN 21050
 21120 J=J+1
 21130 IF J>K THEN 21025
 21140 GOTO 21040
