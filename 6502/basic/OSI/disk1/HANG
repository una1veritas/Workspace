 1 GOTO 2100:--HANGMAN--
 4 REM-----INSTRUCTION choice
 5 PRINTTAB(15);"This is the ":PRINT
 6 PRINTTAB(14);"H A N G M A N":PRINT
 7 PRINTTAB(18)"game":FORI=1 TO 3:PRINT:NEXT
 8 PRINTTAB(5);"DO YOU NEED INSTRUCTIONS (Y/N)? "
 9 POKET,239:IFPEEK(T)=247THENPRINT"Y";:PO=239:PE=247:GOSUB38:GOTO8000
 10 POKET,251:IFPEEK(T)=247THENPRINT"N";:PO=251:PE=247:GOSUB38:GOTO12
 11 GOTO9
 12 REM-----WORD choice
 15 DIML$(26):DIMWR$(21):WR$(4)="CLEVER":WR$(5)="DECENT"
 16 WR$(0)="SPIRIT":WR$(1)="EXOTIC":WR$(2)="SEPARATE"
 17 WR$(6)="LOCATE":WR$(3)="CLOSE":WR$(7)="DENOTE"
 18 WR$(8)="QUININE":WR$(9)="KANGAROO":WR$(10)="REPLICA"
 19 WR$(11)="UGLY":WR$(12)="SERGEANT":WR$(13)="ELBOW"
 20 WR$(14)="MOUSTACHE":WR$(15)="BYE":WR$(16)="FIEND":WR$(17)="KIDNEY"
 21 WR$(18)="SLEEPER":WR$(19)="SCIENTIST":WR$(20)="TRIANGLE"
 24 GOSUB4010:WG=0:RG=0
 25 FORI=1TO26:PRINT:L$(I)=CHR$(I+64):NEXT
 26 PRINT"CHOICES:":PRINT
 27 PRINT"  P  a PLAYER Chooses a word"
 28 PRINT"  C  the COMPUTER chooses a word"
 29 PRINT"  ESC key to break out of the game"
 30 PRINT:PRINT"  Which do you wish? (P,C,ESC)"CHR$(13);
 32 POKET,253:IFPEEK(T)=253THEN PO=253:PE=253:GOSUB38:GOTO50
 34 POKET,251:IFPEEK(T)=191THEN PO=251:PE=191:GOSUB38:GOTO45
 35 GOSUB 4040
 36 I=RND(8):GOTO32
 38 POKET,PO:IFPEEK(T)=PETHEN38
 40 RETURN
 45 I=INT(RND(1)*21):W$=WR$(I):GOTO60
 49 REM-----PERSON chooses
 50 PRINT:PRINT:PRINT:PRINT:PRINT
 52 PRINT:PRINT:PRINT:PRINT:PRINT"  The PLAYER should hide his/her eyes"
 53 PRINT"  while the CHOOSER types the word.":PRINT
 54 PRINT:PRINT"  Press the RETURN key after typing the word.
 55 INPUT"  What is your word";W$
 60 FORI=1TO25:PRINT:NEXT:A=LEN(W$)
 65 GOSUB 4020
 69 REM-----DRAW PICTURE
 70 FORI=GBTOGB+GL:POKEI,161:NEXT:POKEGB+LL+1,161:POKEGB+LL+GL-1,161
 95 GF=GB+GL-1
 96 GT=GF-GH
 100 FORI=GFTOGTSTEP-LL:POKEI,161:NEXT
 110 POKEI+LL-1,177:FORI2=ITOI-GASTEP-1:POKEI2,154:NEXT
 112 GR=GT-GA+LL
 115 POKEGR,140:POKEGR-LL,140:FORI=1TOA:POKELW+I,63:NEXT
 119 REM-----Input text
 120 L1$=STR$(A)+" LETTER WORD"
 130 P=MS-1:GOSUB4000
 150 GOSUB1000:IFL$(IL)="*"THENGOSUB5000:GOTO150
 160 FORI=1TOA:IFMID$(W$,I,1)=L$(IL)THENGOSUB7000
 170 NEXT:L$(IL)="*":MR=1:IFFL=0THENGOSUB6000:GOTO190
 180 FL=0:IFRG=AGOTO700
 185 GOTO150
 190 ONWGGOTO200,210,220,230,240,250,260,270,280,290
 200 POKEBH,226:L1$="HEAD":GOTO800
 210 POKEBH+LL,29:POKEBH+2*LL,31:L1$="BODY":GOTO800
 220 POKEBH+LL-1,195:POKEBH+LL-2,196:L1$="RIGHT ARM":GOTO800
 230 POKEBH+LL+1,197:POKEBH+LL+2,198:L1$=" LEFT":GOTO800
 240 BB=BH+3*LL-1:POKEBB,189:POKEBB+LL-1,189:L1$="RIGHT LEG":GOTO800
 250 POKEBB+2,190:POKEBB+LL+3,190:L1$=" LEFT":GOTO800
 260 POKEBH+LL-3,198:L1$="RIGHT HAND":GOTO800
 270 POKEBH+LL+3,196:L1$=" LEFT":GOTO800
 280 BF=BB+1*LL:POKEBF-2,128:L1$="RIGHT FOOT":GOTO800
 290 POKEBF+4,128:L1$=" LEFT":P=P1:GOSUB4000:P=54592
 300 L1$="I'M AFRAID THAT'S IT!":GOSUB4000:FORI=1TO2000:NEXT
 310 FORI=1TO150:X=INT(RND(1)*1600)+53440:POKEX,42:NEXT
 320 FORI=1TO30:PRINT:NEXT:GOTO740
 700 L1$="YOU WON!                       "
 710 P=MS:GOSUB4000
 735 FORI=1TO1000:NEXT
 740 GOSUB 4020:PRINT"Play again (Y/N)?    ";
 745 POKE(T),239:IFPEEK(T)=247THENPO=239:PE=247:GOSUB38:GOTO 24
 750 PO=251:PE=247:POKE(T),PO
 751 IF PEEK(T)=PE THEN PRINT "N";:GOSUB 38:GOTO49999
 799 GOTO745
 800 REM WRITE MESSAGE AND RETURN
 810 P=P1:GOSUB 4000:GOTO150
 999 REM-----GET LETTER from keyboard
 1000 L1$=" WHAT IS YOUR GUESS?":P=MS+2*LL-1:GOSUB4000
 1001 FORI=1TO5:POKET,R(I):C=PEEK(T)
 1005 IF C<>255 THEN 1015
 1010 NEXT I
 1012 GOSUB 4040
 1013 GOTO1001
 1015 FOR J=1TO7:IF C=R(J) THEN 1020
 1016 NEXT J:GOTO1001
 1020 CH=3*(J+7*I-7)-2
 1022 IL=VAL(MID$(CH$,CH,2))
 1045 L1$="                              "
 1046 P=MS+2*LL:GOSUB 4000
 1050 MR=0:RETURN
 2000 :
 2001 ---HANGMAN---
 2002 :
 2003 DAVID TEWKSBARY
 2004 MODIFIED AUG '81
 2005 :
 2100 GB=53936
 2110 LL=32
 2120 GL=8
 2122 GH=15*LL
 2124 GA=5
 2130 SC=54117
 2135 MS=SC-2*LL
 2137 LW=MS-8*LL
 2140 BH=GB+GL-GH-GA+2*LL-1
 2150 P1=53861-10*LL
 2200 T=57088:POKE 2073,96
 2210 GOSUB 4010
 2215 POKE13026,32
 2300 DIM R(7)
 2310 FOR I=1 TO 7:R(I)=(NOT 2^I)AND255:NEXT
 3000 CH$="16,00,00,00,26,01,17,00,13,14,02,22,03,24,11,10,08,07,06,04"
 3010 CH$=CH$+" 19,09,21,25,20,18,05,23,00,00,00,00,15,12,00"
 3999 GOTO5
 4000 FORI=1TOLEN(L1$):POKEP+I,ASC(MID$(L1$,I,1)):NEXT:RETURN
 4010 DISK!"CA 25A0=11,1":POKE 55296,1:RETURN
 4020 DISK!"CA 25A0=11,2":POKE 55296,0:PRINT
 4030 POKE13026,32:RETURN
 4040 POKET,254:IFPEEK(T)=222THEN20000
 4041 RETURN
 5000 IFMR=1THEN RETURN
 5010 L1$="YOU ALREADY TRIED "+CHR$(IL+64)
 5020 P=MS:GOSUB4000:RETURN
 6000 L1$="SORRY, NO "+CHR$(IL+64)+" HERE.  "
 6010 P=MS:GOSUB4000:WG=WG+1:RETURN
 7000 L1$="VERY GOOD! "+CHR$(IL+64)+" WORKS "
 7010 P=MS:GOSUB4000:FL=1:FORI=1TOA
 7020 IFMID$(W$,I,1)=CHR$(IL+64)THENPOKELW+I,IL+64:RG=RG+1
 7030 NEXT:RETURN
 8000 FORI=1TO10:PRINT:NEXT
 8001 PRINT"In HANGMAN, the PLAYER tries to guess a"
 8005 PRINT"word picked by the CHOOSER.":PRINT
 8010 PRINT"The CHOOSER can either be another person"
 8015 PRINT"or the computer.":PRINT
 8017 GOSUB 8158
 8018 PRINT:PRINT
 8020 PRINT"The PLAYER guesses one letter at a time."
 8025 PRINT"For each wrong guess, the computer will"
 8030 PRINT"draw a part of a body on the scaffold."
 8035 PRINT"Ten wrong guesses and the body is complete"
 8040 PRINT"and the player has been hanged.":PRINT
 8050 PRINT"The computer will tell you when you have"
 8060 PRINT"already tried a letter, and will not"
 8070 PRINT"penalize you for trying it again."
 8075 GOSUB 8160
 8077 PRINT:PRINT:PRINT:PRINT:PRINT
 8080 PRINT"Only letters can appear in the word (no"
 8090 PRINT"hyphens, spaces, or special characters"
 8100 PRINT"please!)"
 8105 PRINT:PRINT:PRINT
 8110 GOSUB 8160
 8115 PRINT:PRINT:PRINT:PRINT:PRINT
 8120 PRINT"At any time, you can end the game by"
 8125 PRINT"pressing the ESC key."
 8130 PRINT:PRINT:PRINT
 8135 GOSUB 8160
 8155 GOTO12
 8158 PRINT"Depress ESC key to end this game."
 8160 PRINT:PRINT"Depress any key to continue."
 8165 POKE 2073,96
 8167 GOSUB 4040
 8170 POKET,0:IF PEEK(T)=254 THEN 8167
 8175 I=RND(8):RETURN
 20000 FORI=1TO24:PRINT:NEXT
 20001 GOSUB 4010
 20002 PRINT"Do you wish to play again <Y/N>?"
 20003 PRINT:PRINT:PRINT
 20004 POKET,239:IFPEEK(T)=247THENPO=239:PE=247:GOSUB38:RUN
 20006 PO=251:PE=247:POKET,PO
 20008 IF PEEK(T)=PE THEN GOSUB 38:GOTO49999
 20009 GOTO20004
 49999 RUN"BEXEC*
