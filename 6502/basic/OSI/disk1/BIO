 1 GOTO1100:---BIORHYTHMS---
 8 PRINTTAB(13)"B I O R H Y T H M"
 9 PRINTTAB(14)"A N A L Y S I S":PRINT
 10 PRINT"This program will print out your biorhythm"
 12 PRINT"analysis for a 22 day period starting at"
 14 PRINT"any date you select.":PRINT
 15 GOSUB 5000:PRINT:PRINT:PRINT:PRINT:PRINT
 16 PRINT"Enter information by typing data, then
 17 PRINT"pressing the RETURN key.":PRINT
 18 PRINT"Dates should be entered in numerical form"
 19 PRINT"with MONTH, DAY, YEAR separated by commas."
 25 PRINT"For example:"
 27 PRINT"JULY 4,1976 would be entered as:"
 30 PRINT"7,4,76":PRINT
 31 PRINT"To leave program, enter 0 instead of data item.
 33 PRINT:GOSUB 5000
 34 N0=1:INPUT"  What is your NAME";A$
 35 PRINT
 36 IFA$="0"THENN0=0:GOTO580
 38 GOTO110
 39 REM-----INPUT START DATE
 40 PRINT"  Enter the start date of the"
 42 PRINT"  analysis (M,D,Y)";
 50 INPUTM,D,Y
 55 IFM=0ORD=0ORY=0THEN580
 60 IFM<1ORM>12ORD<1ORD>31ORY<0ORY>99THEN40
 105 M=INT(M):D=INT(D):Y=INT(Y)
 108 GOTO120
 110 B0=1:PRINT"  What is the date of your"
 111 PRINT"  birth (M,D,Y)";
 112 INPUTMB,DB,YB:PRINT
 113 IFMB=0ORDB=0ORYB=0THENB0=0:GOTO580
 114 IFMB<1ORMB>12ORD<1ORD>31ORY<0ORY>99THEN40
 115 MB=INT(MB):DB=INT(DB):YB=INT(YB)
 118 GOTO40
 120 FORI=1TO5:PRINT:NEXT
 130 PRINT"================================================"
 135 PRINT"                    O H I O"
 137 PRINT"              S C I E N T I F I C"
 138 PRINT
 140 PRINT"            BIORHYTHM DEMONSTRATION"
 145 PRINT
 150 PRINT"================================================"
 160 GOSUB 5000
 161 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
 162 T=INT(D+365.25*Y+A(M)+.01*M-.03)
 170 TB=INT(DB+365.25*YB+A(MB)+.01*MB-.03)
 180 X=T-TB
 184 RB=TB-1-INT((TB-1)/7)*7
 200 REM-----START DISPLAY
 210 PRINT"<CTRL/S> will halt the listing.
 220 PRINT"<CTRL/Q> will continue the listing.
 230 PRINT"<ESC> will leave the analysis.":PRINT
 274 PRINT:PRINT"BIORHYTHM ANALYSIS for "A$
 278 GOTO 302
 280 R=T-1-INT((T-1)/7)*7
 282 FF=LEN(DAY$(R))
 283 LE=21+FF+LEN(M$(M))+LEN(STR$(D))+LEN(RIGHT$(STR$(Y),2))
 284 V2=INT((64-LE)/2)
 285 PRINT"Starting date: ";
 288 PRINTDAY$(R)", "M$(M)STR$(D);
 290 IFY<10THENPRINT", 190";:RC=1
 291 IFY>10THENPRINT", 19";:RC=2
 295 PRINTMID$(STR$(Y),2,RC):PRINT
 299 FG=LEN(DAY$(RB))
 300 LL=FG+LEN(M$(MB))+LEN(STR$(DB))+LEN(RIGHT$(STR$(YB),2))
 301 GOSUB 5000:GOTO308
 302 PRINT"Birth date: "DAY$(RB)", "M$(MB)STR$(DB);
 304 IFYB<10THENPRINT", 190";:RC=1
 305 IFYB>10THENPRINT", 19";:RC=2
 306 PRINTMID$(STR$(YB),2,RC)
 307 GOTO 280
 308 PRINTTAB(7)"DOWN"TAB(22)"CRITICAL"TAB(41)"UP":FL=0
 309 REM-----PRINT GRAPH
 310 PRINTD$(R);D;LEFT$(M$(M),3);
 315 Z=1
 320 F(0)=CL:F$(0)="*"
 330 F(1)=FNA(X/33)
 331 F(1)=CL+F(1):F$(1)="I"
 340 F(2)=FNA(X/28)
 341 F(2)=CL+F(2):F$(2)="E"
 350 F(3)=FNA(X/23)
 351 F(3)=CL+F(3):F$(3)="P"
 360 FORI=0TO2
 370 FORJ=I+1TO3
 380 IFF(I)<F(J)THEN420
 385 IF F(I)>F(J) GOTO 390
 386 F(I)=0:F$(I)=""
 387 F$(J)="X":GOTO 420
 390 Q=F(I):Q$=F$(I)
 400 F(I)=F(J):F$(I)=F$(J)
 410 F(J)=Q:F$(J)=Q$
 420 NEXT J,I
 440 FOR I=0 TO 3
 450 PRINTTAB(F(I));F$(I);
 460 NEXTI
 465 GOSUB8000:IF FG=1THENPRINT:GOTO580
 470 PRINT
 471 X=X+1
 475 IF Z>25 THENGOSUB 7000:Z=0:IF FL=0 THEN 580
 477 REM-----UPDATE DAY, MONTH VARIABLES
 480 R=R+1:IFR=7THENR=0
 485 L=31
 490 IF M=4 OR M=6 OR M=9 OR M=11 THEN L=30
 495 IF M=2 THEN L=28
 500 IF M=2 AND Y/4=INT(Y/4)THEN L=29
 530 D=D+1:IF D>L THEN D=1:M=M+1
 535 IF M=13 THEN M=1
 536 Z=Z+1
 540 IF D=1 THEN PRINT D$(R);D;LEFT$(M$(M),3);
 550 IFD<>1THENPRINTD$(R);D;
 560 GOTO320
 565 REM-----END OF GRAPH DRAWING
 580 G2$="do another graph"
 582 IFN0=0ORB0=0THENG2$="start again"
 584 PRINT"  Do you wish to ";G2$;" (Y/N)? ";
 585 GOSUB7002
 586 IF FL=0 THEN RUN"BEXEC*
 588 IFN0=0THEN34
 590 PRINT"  For the same person? ";:GOSUB 7002
 595 IF FL=0 THEN 34
 597 IFB0=0THEN110
 600 GOTO 40
 900 DATA0,31,59,90,120,151,181,212,243,273,304,334
 910 DATAMONDAY,TUESDAY,WEDNESDAY,THURSDAY,FRIDAY,SATURDAY,SUNDAY
 920 DATAMO,TU,WE,TH,FR,SA,SU
 930 DATAJanuary,February,March,April,May,June,July,August
 940 DATASeptember,October,November,December
 997 RUN"BEXEC*
 998 END
 999 :
 1000 ---BIORHYTHM ANALYSIS---
 1002 :
 1004 :  CIP
 1006 :  REVISED JULY '81
 1008 :
 1100 REM-----INIZ
 1105 DIM A(12),M$(12)
 1107 FORI=1TO25:PRINT:NEXT
 1108 PI=3.14159265
 1109 CL=25:A=18
 1110 DISK!"CA 25A0=11,1":POKE 55296,1
 1120 DEFFNA(Y)=INT(A*SIN(2*PI*Y)+.5)
 1130 FOR I=1 TO 12:READ A(I):NEXT
 1140 FOR I=0 TO 6:READ DAY$(I):NEXT
 1150 FOR I=0 TO 6:READ D$(I):NEXT
 1172 FOR I=1 TO 12:READ M$(I):NEXT
 1900 GOTO8
 4999 REM-----KEY PRESS SUBROUTINES
 5000 PRINT"  Press any key to continue."CHR$(13);
 5002 POKE2073,96:PK=57088
 5004 POKEPK,0:IFPEEK(PK)=254THEN5004
 5005 POKEPK,0:IFPEEK(PK)<>254THEN5005
 5006 POKE2073,173
 5007 PRINT"                                  "CHR$(13);
 5008 RETURN
 6000 DISK!"CA 25A0=11,2":POKE 55296,0:RETURN
 7000 PRINT"  Continue? <Y/N>"CHR$(13);
 7002 FL=-1:POKE2073,96:PK=57088
 7004 POKEPK,239:IFPEEK(PK)=247THEN FL=1
 7006 POKEPK,251:IFPEEK(PK)=247THEN FL=0
 7007 IF FL=-1 THEN 7004
 7008 POKEPK,0:IFPEEK(PK)<>254THEN7008
 7009 POKE2073,173
 7010 PRINT"                         "CHR$(13);
 7012 RETURN
 8000 POKE2073,96:FG=0
 8010 POKEPK,254:IFPEEK(PK)=222THENFG=1
 8020 POKE2073,173:RETURN
