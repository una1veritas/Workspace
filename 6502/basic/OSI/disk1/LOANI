 1 REM--LOAN FINANCE
 2 REM--ROGER VAN SCOY
 3 REM--C1P--
 4 REM MODIFIED JULY '81
 6 CC=2073:PK=57088
 7 RA=253:R2=251:R4=239:C3=247:C4=239:C6=191
 8 FORI=1TO25:PRINT:NEXT
 10 PRINT"     LOAN INTEREST "
 11 PRINT:PRINT:PRINT"      CALCULATION"
 12 FORI=1TO10:PRINT:NEXT
 13 PRINT"Instructions? <Y/N> ";
 14 POKECC,96
 16 POKEPK,R4:IFPEEK(PK)=C3THENPRINT"Y";:PO=R4:PE=C3:GOSUB5226:GOTO25
 18 POKEPK,R2:IFPEEK(PK)=C3THENPRINT"N";:PO=R2:PE=C3:GOSUB5226:GOTO5000
 20 GOTO16
 24 REM--------------INSTRUCTIONS
 25 FORI=1TO30:PRINT:NEXT
 30 PRINT:PRINT:PRINT"Information needed:"
 34 PRINT:PRINT"1) AMOUNT OF LOAN"
 36 PRINT"2) YEARLY INTEREST RATE"
 38 PRINT"3) TYPE OF PAYMENT PLAN"
 40 PRINT
 42 PRINT"Choices:":PRINT
 44 PRINT"1) PICK PAYMENT SIZE"
 45 PRINT"         or"
 46 PRINT"2) NUMBER OF PAYMENTS"
 52 PRINT
 60 PRINT:PRINT:PRINT"Press any key"
 61 PRINT"to continue. ";
 65 POKECC,96
 70 POKEPK,0:IFPEEK(PK)=254THEN70
 75 POKECC,173
 80 GOTO5000
 100 REM ------------ RESULT HEADING SUBROUTINE
 130 FORX=1TO10:PRINT:NEXT
 140 PRINT"LOAN: $";S;"- - YEARLY INT. RATE:";R;"%"
 150 PRINT"TYPE OF PAYMENT PLAN:  ";M$
 160 PRINT"AMOUNT OF ";M$;" PAYMENT:  $";A
 172 IFF1=0THENGOSUB4000
 174 PRINT"PAYMENTS:";N;"- - TOT. INT. $";IT
 190 PRINTTAB(6)"   LOAN"TAB(19)" PAYMENT"TAB(30)" PAYMENT"
 195 PRINTTAB(6)"BALANCE"TAB(19)" TO INT."TAB(30)"TO PRINC."
 198 RETURN
 200 REM ----------- LIST CALCULATION AND PRINT SUBROUTINE
 202 TW=1:TA=1:S1=S:FORC=1TON
 205 I=INT(100*(S1*R2)):I=I/100
 208 T1=INT(100*(A-I)+.5):T1=T1/100
 210 IFT1>=S1THENT1=S1+I
 211 S(1)=S1:S(2)=I:S(3)=T1
 212 FOR I=1 TO 3:S$(I)=STR$(S(I)+.001)
 213 S$(I)=LEFT$(S$(I),LEN(S$(I))-1)
 214 IFLEN(S$(I))<9THENS$(I)=" "+S$(I):GOTO214
 215 NEXT
 217 PRINTC;TAB(5)S$(1);TAB(17)S$(2);TAB(28)S$(3)
 220 TI=TI+I
 230 S1=INT(100*(S1-T1)+.5):S1=S1/100
 235 IFC/TA>4.99THENGOSUB3000
 240 NEXTC
 260 RETURN
 299 REM---------------ERROR SUBROUTINE
 300 PRINT:PRINT
 310 PRINT"PAYMENT FOR EACH PERIOD IS JUST EQUAL TO THE INTEREST"
 320 PRINT"FOR THAT PERIOD.  THERE IS NO WAY YOU CAN REPAY THIS LOAN"
 330 RETURN
 349 REM---------------ERROR SUBROUTINE
 54 ST FOR EACH PERIOD EXCEEDS PAYMENT FOR EACH PERIOD."
 350 PRINT:PRINT
 352 PRINT"INTEREST FOR EACH PERIOD EXCEEDS PAYMENT"
 353 PRINT"FOR EACH PERIOD."
 360 PRINT"IT IS IMPOSSIBLE FOR YOU TO REPAY THIS LOAN."
 370 RETURN
 3000 REM -------------- CONTINUE LIST SUBROUTINE
 3005 IFN/TW=5THEN8999
 3006 TW=TW+1
 3010 PRINT"  PRESS SPACE BAR OR ESC KEY";CHR$(13);
 3020 POKECC,96
 3030 POKEPK,253:IFPEEK(PK)=239THEN3100
 3040 POKEPK,254:IFPEEK(PK)=222THEN3200
 3050 GOTO3030
 3100 POKECC,173:F1=1:TA=TA+1:FORI=11TO12:PRINT:NEXT
 3110 GOSUB130:RETURN
 3200 POKECC,173:GOTO9000
 4000 REM ------------- TOTAL INTEREST CALCULATION SUBROUTINE
 4005 S1=S:PRINT"CALCULATING";:FORT=1TO1000:NEXT:X2=1
 4008 FORC=1TON
 4010 I=INT(100*(S1*R2)):I=I/100
 4020 T1=INT(100*(A-I)+.5):T1=T1/100
 4030 IFT1>=S1THENT1=S1+I
 4040 IT=IT+I
 4050 S1=INT(100*(S1-T1)+.5):S1=S1/100
 4055 IFX2=37THENPRINTCHR$(13);:X2=1
 4060 PRINT" ";
 4065 X2=X2+1
 4070 NEXTC
 4072 IT=INT(100*IT)/100
 4075 PRINTCHR$(13)"           "CHR$(13);
 4080 RETURN
 4999 REM---------------MAIN LOOP
 5000 REM---------------INPUT REQUEST MESSAGES
 5010 REM CONFIGURE SCREEN AS 12X48
 5020 DISK!"CA 25A0=11,1":POKE 55296,1
 5030 FORI=1TO30:PRINT:NEXT
 5050 PRINT"Enter the data asked for, then press"
 5060 PRINT"the RETURN key.":PRINT
 5065 PRINT
 5070 PRINT"To break out of the program, enter 0"
 5080 PRINT"for any data value."
 5095 PRINT:PRINT
 5100 INPUT"What is the amount of the loan";S
 5105 IFS=0THEN9000
 5110 PRINT:INPUT"What is the yearly interest rate in percent";R:PRINT
 5113 IFR=0THEN9000
 5115 IFR>50THENPRINT"YOU'RE BORROWING MONEY FROM A LOAN SHARK.  I'D"
 5117 IFR>50THENPRINT"SUGGEST TRYING A LOCAL BANK.":GOTO5110
 5120 PRINT"What type of payment plan do you want?"
 5121 PRINT
 5122 PRINT"1) WEEKLY"
 5124 PRINT"2) BI-MONTHLY"
 5126 PRINT"3) MONTHLY"
 5128 PRINT"4) QUARTERLY"
 5130 PRINT"5) SEMI-ANNUAL"
 5132 PRINT"6) ANNUAL"
 5140 PRINT
 5150 INPUT"Please enter the number of the plan you want";J
 5155 IFJ=0THEN9000
 5160 IFJ=1THENR1=R/52:M$="WEEKLY":N$="WEEK"
 5170 IFJ=2THENR1=R/26:M$="BI-MONTHLY":N$="2 WEEKS"
 5180 IFJ=3THENR1=R/12:M$="MONTHLY":N$="MONTH"
 5190 IFJ=4THENR1=R/4:M$="QUARTERLY":N$="QUARTER"
 5195 IFJ=5THENR1=R/2:M$="SEMI-ANNUAL":N$="HALF"
 5200 IFJ=6THENR1=R:M$="ANNUAL":N$="YEAR"
 5205 IFJ<0ORJ>6THENPRINT:PRINT"PLEASE ENTER A NUMBER BETWEEN 1 & 6"
 5207 IFJ<0ORJ>6THEN5120
 5208 PRINT
 5210 B=1+R1/100:R2=R1/100
 5212 PRINT"You can choose between specifying the Number of
 5214 PRINT"payments (N) or the Amount of each payment (A).
 5216 PRINT"Make the choice by pressing the N or the A key.";
 5218 POKECC,96
 5220 POKEPK,251:IFPEEK(PK)=247THENPRINT"N";:PO=251:PE=247:GOSUB5226:GOTO5300
 5222 POKEPK,253:IFPEEK(PK)=191THENPRINT"A";:PO=253:PE=191:GOSUB5226:GOTO5240
 5224 GOTO5220
 5225 REM---------------A, AMOUNT
 5226 POKEPK,PO:IFPEEK(PK)=PETHEN5226
 5228 POKECC,173:RETURN
 5240 PRINT:PRINT:INPUT"What is the amount of each payment";A
 5242 IFA=0THEN9005
 5244 IFA-S*R2=0THENGOSUB300:GOTO9000
 5248 IFA-S*R2<0THENGOSUB350:GOTO9000
 5250 N=LOG((S/(A-S*R2))*(B-1)+1)/LOG(B)
 5251 IF N+.001-INT(N)>0THEN N=INT(N)+1
 5260 F1=0:GOSUB130:GOSUB200:GOTO8999
 5299 REM---------------N, NUMBER OF PAYMENTS
 5300 PRINT:PRINT:INPUT"How many payments do you want to make";N
 5301 IFN>480THENPRINT"YOU'D BETTER RECONSIDER THE NUMBER OF PAYMENTS"
 5302 IFN>480THENPRINT"OR NONE OF US WILL BE AROUND TO MAKE
 5303 IFN>480THENPRINT"THE LAST PAYMENT":GOTO5300
 5305 IFN=0THEN9000
 5310 A=INT(100*((S*R2*B^N)/(B^N-1))):A=A/100
 5320 IFA<=0THENGOSUB350:GOTO8999
 5330 F1=0:GOSUB130:GOSUB200
 8998 REM---------------EXIT ROUTINE
 8999 REM
 9000 PRINT"Would you like another run <Y/N>? ";
 9010 POKECC,96
 9020 POKEPK,239:IFPEEK(PK)=247THENPRINT"Y";:PO=239:PE=247:GOSUB5226:GOTO9100
 9030 POKEPK,251:IFPEEK(PK)=247THENPRINT"N";:GOTO9200
 9040 GOTO9020
 9100 CLEAR:CC=2073:PK=57088:GOTO5000
 9200 PRINT:PRINT:PRINT:PRINT"I HOPE I'VE BEEN OF SERVICE.";
 9210 FORI=1TO1000:NEXT
 49999 RUN"BEXEC*
