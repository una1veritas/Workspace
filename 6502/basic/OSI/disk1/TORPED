 10 CLEAR:REM--R. VAN SCOY--TORPEDO
 15 PRINTTAB(8)"TORPEDO":FORI=1TO10:PRINT:NEXT
 18 POKE2073,96:KE=57088
 20 PRINTTAB(5)"Do you want":PRINT:PRINT" instructions (Y/N)? ";
 22 POKEKE,251:IFPEEK(KE)=247THENGOSUB3000:GOTO130
 23 I=RND(1)
 24 POKEKE,239:I=RND(1):IFPEEK(KE)<>247THEN22
 25 FORX=1TO15:PRINT:NEXT
 30 PRINT"Hello, Admiral.":PRINT:PRINT"Your orders have put"
 40 PRINT"you in charge of an":PRINT"underwater, anti-subma-"
 50 PRINT"rine installation.  You":PRINT"are to destroy all"
 60 PRINT"submarines which pass":PRINT"over your command!!!"
 65 PRINT"Because of the impor-":PRINT"tance of the assign-"
 70 PRINT"ment, your performance":PRINT"will be rated, and"
 75 PRINT"you will receive":PRINT"50 merit points for"
 80 PRINT"each sub you destroy.":PRINT"Good luck, Admiral!"
 81 PRINT:PRINT"Keys 1-4 fire your":PRINT"guns.
 83 PRINT:PRINT"PRESS 'C' TO GO ON. ";
 84 POKEKE,251:IFPEEK(KE)<>191THEN84=
 85 FORI=1TO10:PRINT:NEXT
 88 PRINT"Oh! I almost forgot":PRINT"to tell you that due"
 90 PRINT"to budget cuts, you":PRINT"must make every shot"
 93 PRINT"count. Each miss will":PRINT"cost you 5 points, and
 95 PRINT"every sub that gets":PRINT"away will cost 10"
 100 PRINT"points!  You must also":PRINT"know that the subs are"
 105 PRINT"equipped with auto-":PRINT"pilots which are de-
 110 PRINT"signed to confuse the":PRINT"enemy!
 130 M$="TORPEDO:    SCORE: 000"
 140 KE=57088
 150 FORX=1TO4:READT(X):T(X)=T(X)-32:NEXT
 170 DIMR(14):FORX=1TO14:READR(X):NEXT
 180 DEFFNC(X)=INT(8-LOG(X)/LOG(2)+.05)
 200 PRINT:PRINT"Press 'ESC' to exit the":PRINT"game at any time.
 210 PRINT:PRINT"Press 'C' to go on. ";
 220 POKEKE,254:IFPEEK(KE)=222THEN1900
 225 POKEKE,251:IFPEEK(KE)<>191THEN220
 230 GOSUB3000
 240 T=54118:C1=49:T1=T+10
 245 NU=25:SC=0
 250 TP=53411:H=544:POKE54117,32
 270 FORX=1TOLEN(M$):POKETP+X,ASC(MID$(M$,X,1)):NEXT
 290 FORX=53443TO53466STEP2:POKEX,196:POKEX+1,198:NEXT
 300 FORX=TTOT+21STEP6:POKEX,213:POKEX+1,163:POKEX+2,212
 310 POKEX+65,C1:C1=C1+1:NEXT
 340 FL=0
 350 Z=INT(15*RND(1))*32+53540:Z2=INT(14*RND(1)+1)
 400 C=Z:C1=Z+22
 405 POKET1,32:POKET1-1,32:T1=C+R(Z2)
 410 POKET1,8:POKET1-1,7
 420 S1=INT(NU/10):S2=INT(NU-S1*10):POKETP+10,S1+48:POKETP+11,S2+48
 430 IFFL=1THEN520
 435 POKEKE,254:IFPEEK(KE)=222THEN1900
 450 POKEKE,127:S=254-PEEK(KE):IFS<=8THEN680
 470 S=FNC(S):FL=1
 500 Q=T(S):Q1=T(S)-H
 520 POKEQ-32,232:POKEQ,32
 590 IFPEEK(Q-64)=8ORPEEK(Q-64)=7THENNU=NU-1:GOTO900
 650 Q=Q-32:IFQ>Q1THEN680
 655 FL=0
 660 POKEQ,32:NU=NU-1:SC=SC-5:GOSUB920:IFNU=0THEN1000
 680 Z2=INT(14*RND(1)+1)
 700 C=C+1:IFC<=C1THEN405
 710 POKET1-2,32:POKET1-1,32
 750 SC=SC-10:GOSUB920:GOTO350
 899 REM---EXPLOSION OF SUB
 900 POKEQ-32,32
 910 FORQ1=0TO32:POKET1-1,Q1:POKET1,Q1:NEXT
 915 SC=SC+50:GOSUB920:IFNU=0THEN1000
 916 FL=0:GOTO350
 920 L=ABS(SC):S3=INT(L/100):S4=INT((L-100*S3)/10):S5=L-100*S3-10*S4
 925 IFSC<0THENPOKETP+20,45
 926 IFSC>=0THENPOKETP+20,32
 930 POKETP+21,S3+48:POKETP+22,S4+48:POKETP+23,S5+48
 950 RETURN
 1000 PRINT:PRINT:PRINT:IFSC<150THEN1100
 1010 PRINT"Congratulations,":PRINT"Admiral.  With";SC
 1020 PRINT"points you're America's":PRINT"first submarine ace!!"
 1030 GOTO1600
 1100 IFSC<100THEN1200
 1110 PRINTSC;"points is excel-":PRINT"lent shooting, Admiral."
 1120 PRINT"It's good enough to":PRINT"get you promoted.":GOTO1600
 1200 IFSC<50THEN1300
 1210 PRINT"Well, Admiral,";SC:PRINT"points is pretty good"
 1215 PRINT"shooting, but the":PRINT"top man thinks you"
 1220 PRINT"need more practice.":GOTO1600
 1300 IFSC<0THEN1400
 1310 PRINTSC;"points is not":PRINT"very good, Admiral."
 1320 PRINT"The country is lucky":PRINT"it's not depending"
 1330 PRINT"on you!":GOTO1600
 1400 PRINT"When you get only":PRINTSC;"points, the enemy"
 1410 PRINT"is glad you're not":PRINT"on their side.  You'll"
 1420 PRINT"be mopping the decks":PRINT"tomorrow, if you don't"
 1440 PRINT"improve that score!!"
 1600 PRINT:PRINT:PRINT"Do you want to try"
 1630 PRINT"again (Y/N)? ";
 1640 POKEKE,239:IFPEEK(KE)=247THENGOSUB3000:RUN130
 1650 POKEKE,251:IFPEEK(KE)<>247THEN1640
 1900 RUN"BEXEC*
 2000 DATA54119,54125,54131,54137
 2100 DATA-65,-64,-63,-33,-32,-31,-1,1,31,32,33,63,64,65
 3000 FORI=1TO25:PRINT:NEXT:RETURN
