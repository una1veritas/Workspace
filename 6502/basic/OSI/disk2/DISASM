 10 INPUT"INPUT STARTING DISASSEMBLY ADDRESS IN DECIMAL";A1
 20 INPUT"INPUT ENDING ADDRESS";A2
 30 PRINTTAB(10);"OCTAL";TAB(18);"HEX";
 35 PRINTTAB(23);"CODE";TAB(33);"OP";TAB(40);
 40 PRINT"OPRAND":PRINT
 60 DIMOT$(151),MN$(56),N$(15)
 80 GOSUB1160:REM INITIALIZE ARRAYS
 90 W=6:BA%=8:OC%=A1:GOSUB1530
 100 OA$=OC$
 110 W=4:BA%=16:OC%=A1:GOSUB1530
 120 AD$=OC$
 130 W=2:OC%=PEEK(A1):GOSUB1530
 140 GOSUB1060
 150 IFFL=1THEN200
 160 GOSUB230
 170 A1=A1+VAL(NB$)
 180 IFA1<=A2THEN90
 190 GOTO220
 200 PRINTTAB(10);OA$;TAB(18);AD$;TAB(23);OC$
 210 A1=A1+1:GOTO90
 220 PRINT"END OF DISASSEMBLY":END
 230 W=2
 240 IFTY$<>"IM"THEN290
 250 OC%=PEEK(A1+1):GOSUB1530
 260 PRINTTAB(10);OA$;TAB(18);AD$;
 265 PRINTTAB(23);O1$;OC$;TAB(33);MN$(VAL(PT$));
 270 PRINTTAB(40);"#$";OC$
 280 RETURN
 290 IFTY$<>"Z "THEN340
 300 OC%=PEEK(A1+1):GOSUB1530
 310 PRINTTAB(10);OA$;TAB(18);AD$;
 315 PRINTTAB(23);O1$;OC$;TAB(33);MN$(VAL(PT$));
 320 PRINTTAB(40);"$";OC$
 330 RETURN
 340 IFTY$<>"ZX"THEN390
 350 OC%=PEEK(A1+1):GOSUB1530
 360 PRINTTAB(10);OA$;TAB(18);AD$;
 365 PRINTTAB(23);O1$;OC$;TAB(33);MN$(VAL(PT$));
 370 PRINTTAB(40);"$";OC$;",X"
 380 RETURN
 390 IFTY$<>"ZY"THEN440
 400 OC%=PEEK(A1+1):GOSUB1530
 410 PRINTTAB(10);OA$;TAB(18);AD$;
 415 PRINTTAB(23);O1$;OC$;TAB(33);MN$(VAL(PT$));
 420 PRINTTAB(40);"$";OC$;",Y"
 430 RETURN
 440 IFTY$<>"A "THEN530
 450 OC%=PEEK(A1+1):GOSUB1530
 460 PRINTTAB(10);OA$;TAB(18);AD$;TAB(23);O1$;OC$;
 470 CO$=OC$:OC%=PEEK(A1+2):GOSUB1530
 480 PRINTOC$;TAB(33);MN$(VAL(PT$));TAB(40);"$";OC$;CO$;
 490 IFO1$="4C"ORO1$="6C"THENPRINTTAB(55);"** JMP **":GOTO520
 500 IFO1$="20"THENPRINTTAB(55);"** JSR **":GOTO520
 510 PRINT
 520 RETURN
 530 IFTY$<>"AX"THEN590
 540 OC%=PEEK(A1+1):GOSUB1530
 550 PRINTTAB(10);OA$;TAB(18);AD$;TAB(23);O1$;OC$;
 560 CO$=OC$:OC%=PEEK(A1+2):GOSUB1530
 570 PRINTOC$;TAB(33);MN$(VAL(PT$));TAB(40);"$";OC$;CO$;",X"
 580 RETURN
 590 IFTY$<>"AY"THEN650
 600 OC%=PEEK(A1+1):GOSUB1530
 610 PRINTTAB(10);OA$;TAB(18);AD$;TAB(23);O1$;OC$;
 620 CO$=OC$:OC%=PEEK(A1+2):GOSUB1530
 630 PRINTOC$;TAB(33);MN$(VAL(PT$));TAB(40);"$";OC$;CO$;",Y"
 640 RETURN
 650 IFTY$<>"IX"THEN700
 660 OC%=PEEK(A1+1):GOSUB1530
 670 PRINTTAB(10);OA$;TAB(18);AD$;
 675 PRINTTAB(23);O1$;OC$;TAB(33);MN$(VAL(PT$));
 680 PRINTTAB(40);"($";OC$;",X)"
 690 RETURN
 700 IFTY$<>"IY"THEN750
 710 OC%=PEEK(A1+1):GOSUB1530
 720 PRINTTAB(10);OA$;TAB(18);AD$;
 725 PRINTTAB(23);O1$;OC$;TAB(33);MN$(VAL(PT$));
 730 PRINTTAB(40);"($";OC$;"),Y"
 740 RETURN
 750 IFTY$<>"IP"THEN780
 760 PRINTTAB(10);OA$;TAB(18);AD$;
 765 PRINTTAB(23);O1$;TAB(33);MN$(VAL(PT$))
 770 RETURN
 780 IFTY$<>"I "THEN860
 790 OC%=PEEK(A1+1):GOSUB1530
 800 PRINTTAB(10);OA$;TAB(18);AD$;TAB(23);O1$;OC$;
 810 CO$=OC$:OC%=PEEK(A1+2):GOSUB1530
 820 PRINTOC$;TAB(33);MN$(VAL(PT$));TAB(40);"($";OC$;CO$;")";
 830 IFO1$="6C"THENPRINTTAB(55);"** JMP **":GOTO850
 840 PRINT
 850 RETURN
 860 IFTY$<>"R "THEN1030
 870 OC%=PEEK(A1+1):GOSUB1530
 880 SS=PEEK(A1+1)
 890 IF SS<128 THEN OC%=A1+2+SS
 900 IF SS>=128 THEN OC%=A1+SS-254
 960 PRINTTAB(10);OA$;TAB(18);AD$;
 965 PRINTTAB(23);O1$;OC$;TAB(33);MN$(VAL(PT$));
 975 W=4
 980 GOSUB1530
 990 PRINTTAB(40);"$";OC$
 1020 RETURN
 1030 PRINTTAB(10);OA$;TAB(18);AD$;
 1035 PRINTTAB(23);O1$;TAB(33);MN$(VAL(PT$));TAB(40);
 1040 PRINT"A"
 1050 RETURN
 1060 FL=0:J0=1:J1=151
 1070 J=INT((J0+J1)/2)
 1080 OZ$=MID$(OT$(J),1,2)
 1082 IF OC$=OZ$THEN 1090
 1083 IF ABS(J1-J0)<2THENFL=1:RETURN
 1084 IF OC$<OZ$THEN J1=J
 1086 IF OC$>OZ$THEN J0=J
 1089 GOTO 1070
 1090 O1$=OC$
 1100 TY$=MID$(OT$(J),3,2)
 1110 NB$=MID$(OT$(J),5,1)
 1120 PT$=MID$(OT$(J),6,2)
 1130 RETURN
 1160 REM TABLE DATA
 1170 FORI=1TO151:READOT$(I):NEXTI
 1180 FORI=1TO56:READMN$(I):NEXTI
 1190 FORI=0TO15:READN$(I):NEXTI
 1195 RETURN
 1200 DATA"00IP111","01IX235","05Z 235","06Z 203","08IP137","09IM235"
 1210 DATA"0AAC103","0DA 335","0EA 303","10R 210","11IY235","15ZX235"
 1220 DATA"16ZX203","18IP114","19AY335","1DAX335","1EAX303","20A 329"
 1230 DATA"21IY202","24Z 207","25Z 202","26Z 240","28IP139","29IM202"
 1240 DATA"2AAC140","2CA 307","2DA 302","2EA 340","30R 208","31IY202"
 1250 DATA"35ZX202","36ZX240","38IP145","39AY302","3DAX302","3EAX340"
 1260 DATA"40IP142","41IX224","45Z 224","46Z 233","48IP136","49IM224"
 1270 DATA"4AAC133","4CA 328","4DA 324","4EA 333","50R 212","51IY224"
 1280 DATA"55ZX224","56ZX233","58IP116","59AY324","5DAX324","5EAX333"
 1290 DATA"60IP143","61IX201","65Z 201","66Z 241","68IP138","69IM201"
 1300 DATA"6AAC141","6CI 328","6DA 301","6EA 341","70R 213","71IY201"
 1310 DATA"75ZX201","76ZX241","78IP147","79AY301","7DAX301","7EAX341"
 1320 DATA"81IX248","84Z 250","85Z 248","86Z 249","88IP123","8AIP155"
 1330 DATA"8CA 350","8DA 348","8EA 349","90R 204","91IY248","94ZX250"
 1340 DATA"95ZX248","96ZY249","98IP153","99AY348","9AIP156","9DAX348"
 1350 DATA"A0IM232","A1IX230","A2IM231","A4Z 232","A5Z 230","A6Z 231"
 1360 DATA"A8IP152","A9IM230","AAIP151","ACA 332","ADA 330","AEA 331"
 1370 DATA"B0R 205","B1IY230","B4ZX232","B5ZX230","B6ZY231","B8IP117"
 1380 DATA"B9AY330","BAIP154","BCAX332","BDAX330","BEAY331","C0IM220"
 1390 DATA"C1IX218","C4Z 220","C5Z 218","C6Z 221","C8IP127","C9IM218"
 1400 DATA"CAIP122","CCA 320","CDA 318","CEA 321","D0R 209","D1IY218"
 1410 DATA"D5ZX218","D6ZX221","D8IP115","D9AY218","DDAX318","DEAX321"
 1420 DATA"E0IM219","E1IX244","E4Z 219","E5Z 244","E6Z 225","E8IP126"
 1430 DATA"E9IM244","EAIP134","ECA 319","EDA 344","EEA 325","F0R 206"
 1440 DATA"F1IY244","F5ZX244","F6ZX225","F8IP146","F9AY344","FDAX344"
 1450 DATA"FEAX325"
 1460 DATA"ADC","AND","ASL","BCC","BCS","BEQ","BIT","BMI","BNE","BPL"
 1470 DATA"BRK","BVC","BVS","CLC","CLD","CLI","CLV","CMP","CPX","CPY"
 1480 DATA"DEC","DEX","DEY","EOR","INC","INX","INY","JMP","JSR","LDA"
 1490 DATA"LDX","LDY","LSR","NOP","ORA","PHA","PHP","PLA","PLP","ROL"
 1500 DATA"ROR","RTI","RTS","SBC","SEC","SED","SEI","STA","STX","STY"
 1510 DATA"TAX","TAY","TYA","TSX","TXA","TXS"
 1520 DATA "0","1","2","3","4","5","6","7","8","9"
 1525 DATA "A","B","C","D","E","F"
 1530 REM DECIMAL TO HEX
 1560 OC$=""
 1570 Q=INT(OC%/BA%)
 1580 R=OC%-BA%*Q
 1590 OC$=N$(R)+OC$:OC%=Q
 1600 IF Q<>0 THEN 1570
 1610 IFLEN(OC$)<W THEN OC$="0"+OC$:GOTO1610
 1620 RETURN
