 10 REM    GENERALIZED FILE TRANSFER UTILITY
 20 REM     10/14/79, REV 8/26/81
 30 P$="/8":S$="327E":DR$="12"
 40 FORI=1TO5:PRINT:NEXT
 60 DEF FNB(X)=10*INT(X/16)+X-16*INT(X/16)
 70 PRINT"** DATA FILE TRANSFER UTILITY **":PRINT:PRINT
 75 PRINT"PLACE SOURCE DISK IN DESIRED DRIVE":PRINT
 80 INPUT"INPUT FILE NAME";IN$:T=LEN(IN$):IFT>0THENT=ASC(IN$)
 90 IFT<65ORT>90GOTO80
 100 GOSUB1000:D1$=D$:PRINT:PRINT
 110 DISK!"SE "+D$:N1$=IN$:GOSUB2000
 120 IFS=0THENPRINT:PRINT"FILE NOT FOUND.":GOTO49999
 130 IB=T0:IE=T9
 135 PRINT"PLACE DESTINATION DISK IN DESIRED DRIVE":PRINT
 140 PRINT:INPUT"OUTPUT FILE NAME";OU$:T=LEN(OU$):IFT>0THENT=ASC(OU$)
 150 IFT<65ORT>90GOTO140
 160 GOSUB1000:PRINT:PRINT
 170 IFD$<>D1$THENDISK!"SE "+D$
 180 N1$=OU$:GOSUB2000:IFS=0GOTO120
 190 IF (IE-IB) > (T9-T0) THENGOSUB3000
 200 OF=IB-T0:PRINT:INPUT"TYPE 'Y' WHEN READY";Y$
 210 FORI=IBTOIE
 215 IFD$=D1$THENGOSUB5000
 220 IFD$<>D1$THENDISK!"SE "+D1$
 230 T=I:GOSUB4000
 240 DISK!"CA "+S$+"="+T$+",1"
 245 IFD1$=D$THENGOSUB5100
 250 IFD$<>D1$THENDISK!"SE "+D$
 260 T=I-OF:GOSUB4000
 270 DISK!"IN "+T$
 280 DISK!"SA "+T$+",1="+S$+P$
 290 NEXTI
 300 PRINT:PRINT"OPERATIONS COMPLETE.":GOTO49999
 1000 PRINT:INPUT"LOCATED ON WHICH DEVICE (A,B,C,D)";D$
 1010 D=LEN(D$):IFD>0THEND=ASC(D$)
 1020 IFD<65ORD>68GOTO1000
 1030 D$=CHR$(D):RETURN
 2000 N1$=N1$+"      ":N1$=LEFT$(N1$,6)
 2010 Z$=DR$+",1":GOSUB2040:IFS=1THENRETURN
 2020 Z$=DR$+",2":GOTO 2040
 2040 DISK!"CA 2E79="+Z$
 2050 S=0:FORI=11897TO12145STEP8:N$=""
 2060 FORJ=0TO5:N$=N$+CHR$(PEEK(I+J)):NEXT
 2070 IFN$<>N1$THENNEXTI:RETURN
 2080 S=1:T0=PEEK(I+6):T9=PEEK(I+7)
 2090 T0=FNB(T0):T9=FNB(T9):RETURN
 3000 PRINT:PRINT:PRINT"   <<< WARNING >>>":PRINT
 3010 PRINT"INPUT FILE LENGTH EXCEEDS OUTPUT FILE LENGTH"
 3020 PRINT"BY"ABS(T9-T0-IE+IB)"TRACKS.  PROGRAM MAY ONLY"
 3030 PRINT"OUTPUT FILE TO"T9-T0+1"TRACKS."
 3040 PRINT:INPUT"CONTINUE (Y OR N)";Y$:IFY$="N"THENEND
 3050 IE=IB+T9-T0:RETURN
 4000 T$=MID$(STR$(T),2):IFT<10THENT$="0"+T$
 4010 RETURN
 5000 PRINT:PRINT"PLACE SOURCE DISK IN DRIVE ";D$
 5010 INPUT"ENTER 'C' TO CONTINUE";X$:RETURN
 5100 PRINT:PRINT"PLACE DESTINATION DISK IN DRIVE ";D$
 5110 INPUT"ENTER 'C' TO CONTINUE";X$:RETURN
 49999 PRINT:PRINT:END
